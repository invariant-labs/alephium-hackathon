struct Tick {
    mut sign: Bool,
    mut liquidityChange: U256,
    mut liquidityGross: U256,
    mut sqrtPrice: U256,
    mut feeGrowthOutsideX: U256,
    mut feeGrowthOutsideY: U256,
    mut secondsOutside: U256
}

Abstract Contract TickHelper() extends Decimal() {
    enum TickError {
        InvalidTickLiquidity = 1001
    }

    fn updateTick(mut tick: Tick, liquidityDelta: U256, maxLiquidityPerTick: U256, isUpper: Bool, isDeposit: Bool) -> Tick {
        tick.liquidityGross = calculateNewLiquidityGross(tick, isDeposit, liquidityDelta, maxLiquidityPerTick)
        let updateLiquiditySign = isDeposit && !isUpper || !isDeposit && isUpper
        return updateLiquidityChange(tick, liquidityDelta, updateLiquiditySign)
    }

    fn cross(mut tick: Tick,feeGrowthGlobalX: U256, feeGrowthGlobalY: U256, currentTick: I256, index: I256) -> (Bool, Tick) {
        tick.feeGrowthOutsideX = wrappingSub(feeGrowthGlobalX, tick.feeGrowthOutsideX)
        tick.feeGrowthOutsideY = wrappingSub(feeGrowthGlobalY, tick.feeGrowthOutsideY)

        let isBelowCurrent = currentTick >= index

        return (isBelowCurrent && !tick.sign || !isBelowCurrent && tick.sign), tick
    }

    fn updateLiquidityChange(mut tick: Tick, liquidityDelta: U256, add: Bool) -> Tick {
        if (tick.sign && !add || !tick.sign && add) {
            if (tick.liquidityChange > liquidityDelta) {
                tick.liquidityChange = tick.liquidityChange - liquidityDelta
            } else {
                tick.liquidityChange = liquidityDelta - tick.liquidityChange
                tick.sign = !tick.sign
            }
        } else {
            tick.liquidityChange = tick.liquidityChange + liquidityDelta
        }
        
        return tick
    }

    fn calculateNewLiquidityGross(tick: Tick, sign: Bool, liquidityDelta: U256, maxLiquidityPerTick: U256) -> U256 {
        let mut newLiquidity = 0
        if (sign) {
            newLiquidity = tick.liquidityGross + liquidityDelta
        } else {
            newLiquidity = tick.liquidityGross - liquidityDelta
        }

        assert!(!sign || newLiquidity < maxLiquidityPerTick, TickError.InvalidTickLiquidity)

        return newLiquidity
    }
}