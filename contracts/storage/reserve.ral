Contract Reserve(invariant: Address, mut assetsStored: U256) {
    const MaxAssetsStored = 8
    
    enum ReserveError {
        InvalidCaller = 2001
        InvalidValue = 2002
    }

    @using(updateFields = true)
    pub fn setStoredAssets(v: U256) -> () {
        let caller = callerAddress!()
        checkCaller!(invariant == caller, ReserveError.InvalidCaller)
        assert!(v <= MaxAssetsStored, ReserveError.InvalidValue)
        assetsStored = v
    }

    pub fn getAssetsCount() -> U256 {
        let caller = callerAddress!()
        checkCaller!(invariant == caller, ReserveError.InvalidCaller)
        return assetsStored
    }

    @using(preapprovedAssets = true, assetsInContract = true)
    pub fn depositSingleAsset(from: Address, id: ByteVec, amount: U256) -> () {
        let caller = callerAddress!()
        checkCaller!(invariant == caller, ReserveError.InvalidCaller)
        transferTokenToSelf!(from, id, amount)
    }

    @using(preapprovedAssets = true, assetsInContract = true)
    pub fn depositTwoAssets(from: Address, xId: ByteVec, yId: ByteVec, x: U256, y: U256) -> () {
        let caller = callerAddress!()
        checkCaller!(invariant == caller, ReserveError.InvalidCaller)
        transferTokenToSelf!(from, xId, x)
        transferTokenToSelf!(from, yId, y)
    }

    @using(assetsInContract = true)
    pub fn withdrawSingleAsset(to: Address, id: ByteVec, amount: U256) -> () {
        let caller = callerAddress!()
        checkCaller!(invariant == caller, ReserveError.InvalidCaller)
        transferTokenFromSelf!(to, id, amount)
    }

    @using(assetsInContract = true)
    pub fn withdrawTwoAssets(to: Address, xId: ByteVec, yId: ByteVec, x: U256, y: U256) -> () {
        let caller = callerAddress!()
        checkCaller!(invariant == caller, ReserveError.InvalidCaller)
        transferTokenFromSelf!(to, xId, x)
        transferTokenFromSelf!(to, yId, y)
    }

    @using(preapprovedAssets = true, assetsInContract = true)
    pub fn swap(swapper: Address, inId: ByteVec, outId: ByteVec, in: U256, out: U256) -> () {
        let caller = callerAddress!()
        checkCaller!(invariant == caller, ReserveError.InvalidCaller)
        transferTokenToSelf!(swapper, inId, in)
        transferTokenFromSelf!(swapper, outId, out)
    }
}