struct InvariantConfig { admin: Address, mut protocolFee: U256 }

struct SwapResult {
    nextSqrtPrice: U256,
    amountIn: U256,
    amountOut: U256,
    feeAmount: U256
}

struct CalculateSwapResult {
    amountIn: U256,
    amountOut: U256,
    startSqrtPrice: U256,
    targetSqrtPrice: U256,
    fee: U256
}

struct QuoteResult {
    amountIn: U256,
    amountOut: U256,
    targetSqrtPrice: U256
}

Contract Invariant(
    mut config: InvariantConfig,
    clamm: CLAMM,
    mut feeTierCount: U256,
    mut poolKeyCount: U256
) extends PoolKeyHelper(), Decimal(), FeeTiers(), PoolKeys(), Pools(clamm), Ticks(), Tickmap(), Positions(clamm), FeeTierHelper() {
    // Key: feeTierCount
    mapping[U256, FeeTier] feeTiers
    // Key: poolKeyCount
    mapping[U256, PoolKey] poolKeys
    // Key: poolKey
    mapping[ByteVec, Pool] pools
    // Key: poolKey ++ index
    mapping[ByteVec, Tick] ticks
    // Key: poolKey ++ subcontract index
    mapping[ByteVec, TickmapBatch] bitmap
    // Key: owner ++ index
    mapping[ByteVec, Position] positions
    // Key: owner
    mapping[Address, U256] positionsCounter
    
    enum InvariantError {
        NotAdmin = 0
        InsufficientLiquidity = 1
        InvalidTickSpacing = 2
        InvalidFee = 3
        FeeTierNotFound = 4
        TokensAreSame = 5
        PoolKeyAlreadyExist = 6
        TickAndSqrtPriceMismatch = 7
        NotFeeReceiver = 8
        InvalidTickLiquidity = 9
        ZeroLiquidity = 10
        PriceLimitReached = 11
        InvalidProtocolFee = 12
        NotOwner = 13
        ZeroAmount = 14
        WrongPriceLimit = 15
        NoGainSwap = 16
        PositionDoesNotExist = 17
        FeeTierAlreadyExist = 18
        PoolDoesNotExist = 19
        PoolAlreadyExist = 20
        TickAlreadyExist = 21
        InvalidTickIndex = 22
        TickAndTickSpacingMismatch = 23
        TickLimitReached = 24
        ChunkNotFound = 25
        TickInitialized = 26
        PositionNotFound = 27
        EmptyPositionPokes = 28
    }
    
    pub fn getProtocolFee() -> U256 {
        return config.protocolFee
    }

    @using(updateFields = true)
    pub fn changeProtocolFee(newProtocolFee: U256) -> () {
        checkCaller!(callerAddress!() == config.admin, InvariantError.NotAdmin)
        assert!(newProtocolFee < one(PercentageScale), InvariantError.InvalidProtocolFee)
        config.protocolFee = newProtocolFee
    }

    @using(assetsInContract = true)
    pub fn withdrawProtocolFee(poolKey: PoolKey) -> () {
        let caller = callerAddress!()
        let (exist, mut pool) = wrappedGetPool(poolKey)
        assert!(exist, InvariantError.PoolDoesNotExist)

        let feeReceiver = pool.feeReceiver

        checkCaller!(feeReceiver == caller, InvariantError.NotFeeReceiver)

        let x = pool.feeProtocolTokenX
        let y = pool.feeProtocolTokenY
        pool.feeProtocolTokenX = 0
        pool.feeProtocolTokenY = 0

        rewritePool(pool)

        transferTokenFromSelf!(caller, pool.poolKey.tokenX, x)
        transferTokenFromSelf!(caller, pool.poolKey.tokenY, y)
    }

    pub fn changeFeeReceiver(poolKey: PoolKey, newFeeReceiver: Address) -> () {
        let caller = callerAddress!()

        checkCaller!(config.admin == caller, InvariantError.NotAdmin)

        let key = poolKeyBytes(poolKey)
        let mut pool = pools[key]

        pool.feeReceiver = newFeeReceiver
        pools[key] = pool
    }

    @using(preapprovedAssets = true, checkExternalCaller = false)
    pub fn createPool(token0: ByteVec, token1: ByteVec, feeTier: FeeTier, initSqrtPrice: U256, initTick: I256) -> () {
        let feeTierExist = containsFeeTier(feeTier)
        assert!(feeTierExist, InvariantError.FeeTierNotFound)

        clamm.checkTick(initTick, feeTier.tickSpacing)

        assert!(token0 != token1, InvariantError.TokensAreSame)

        let poolKey = newPoolKey(token0, token1, feeTier)
        
        let poolKeyExist = containsPoolKey(poolKey)
        assert!(!poolKeyExist, InvariantError.PoolKeyAlreadyExist)

        assert!(clamm.checkTickToSqrtPriceRelationship(initTick, feeTier.tickSpacing, initSqrtPrice), InvariantError.TickAndSqrtPriceMismatch)

        let caller = callerAddress!()
        let timestamp = blockTimeStamp!()
        let mapEntryDeposit = mapEntryDeposit!()
        addPoolKey{caller -> ALPH: mapEntryDeposit}(caller, poolKey)
        addPool{caller -> ALPH: mapEntryDeposit}(caller, poolKey, initSqrtPrice, initTick, timestamp, config.admin)
    }

    @using(preapprovedAssets = true)
    pub fn addFeeTier(feeTier: FeeTier) -> () {
        let caller = callerAddress!()
        checkCaller!(caller == config.admin, InvariantError.NotAdmin)

        assert!(feeTier.tickSpacing > 0 && feeTier.tickSpacing <= 100, InvariantError.InvalidTickSpacing)
        assert!(feeTier.fee < one(PercentageScale), InvariantError.InvalidFee)

        wrappedAddFeeTier{caller -> ALPH: mapEntryDeposit!()}(caller, feeTier)
    }

    
    pub fn removeFeeTier(feeTier: FeeTier) -> () {
        let caller = callerAddress!()
        checkCaller!(caller == config.admin, InvariantError.NotAdmin)

        wrappedRemoveFeeTier(caller, feeTier)
    }

    pub fn feeTierExist(feeTier: FeeTier) -> Bool {
        return containsFeeTier(feeTier)
    }

    pub fn getFeeTiers() -> ByteVec {
        return getAllFeeTiers()
    }

    pub fn getPools() -> ByteVec {
        return getAllPoolKeys()
    }

    pub fn getTick(poolKey: PoolKey, index: I256) -> (Bool, Tick) {
        return wrappedGetTick(poolKey, index)
    }

    pub fn getPool(poolKey: PoolKey) -> (Bool, Pool) {
        return wrappedGetPool(poolKey)
    }

    pub fn getPosition(owner: Address, index: U256) -> (Bool, Position) {  
        return wrappedGetPosition(owner, index)
    }

    pub fn isTickInitialized(poolKey: PoolKey, index: I256) -> Bool {
        return getBit(index, poolKey)    
    }

    @using(preapprovedAssets = true)
    fn createTick(
        caller: Address,
        poolKey: PoolKey,
        index: I256,
        currentTickIndex: I256,
        feeGrowthGlobalX: U256,
        feeGrowthGlobalY: U256,
        startTimestamp: U256
    ) -> () {
        clamm.checkTick(index, poolKey.feeTier.tickSpacing)

        let mut feeGrowthOutsideX = 0
        let mut feeGrowthOutsideY = 0
        let mut secondsOutside = 0

        if (index <= currentTickIndex) {
            feeGrowthOutsideX = feeGrowthGlobalX
            feeGrowthOutsideY = feeGrowthGlobalY
            secondsOutside = blockTimeStamp!() - startTimestamp
        }

        wrappedCreateTick{caller -> ALPH: mapEntryDeposit!()}(
            caller,
            poolKey,
            index,
            true,
            0,
            0,
            clamm.calculateSqrtPrice(index),
            feeGrowthOutsideX,
            feeGrowthOutsideY, 
            0
        )
    }

    @using(preapprovedAssets = true, assetsInContract = true, checkExternalCaller = false)
    pub fn createPosition(
        poolKey: PoolKey,
        lowerTickIndex: I256,
        upperTickIndex: I256,
        liquidityDelta: U256,
        slippageLimitLower: U256,
        slippageLimitUpper: U256
    ) -> () {
        let (exist, pool) = wrappedGetPool(poolKey)
        assert!(exist, InvariantError.PoolDoesNotExist)
        assert!(liquidityDelta != 0, InvariantError.ZeroLiquidity)
        assert!(lowerTickIndex != upperTickIndex, InvariantError.InvalidTickIndex)

        let currentTimestamp = blockTimeStamp!()
        let mapEntryDeposit = mapEntryDeposit!()
        let mapEntry2Deposits = mapEntryDeposit * 2
        let caller = callerAddress!()

        let (lowerChunk, upperChunk) = getChunks(lowerTickIndex, upperTickIndex, poolKey.feeTier.tickSpacing)

        if (lowerChunk == upperChunk) {
            initializeChunk{caller -> ALPH: mapEntryDeposit}(caller, poolKey, lowerChunk)
        } else {
            initializeChunk{caller -> ALPH: mapEntryDeposit}(caller, poolKey, lowerChunk)
            initializeChunk{caller -> ALPH: mapEntryDeposit}(caller, poolKey, upperChunk)
        }
        
        if (!containsTick(poolKey, lowerTickIndex)) {
            createTick{caller -> ALPH: mapEntry2Deposits}(caller, poolKey, lowerTickIndex, pool.currentTickIndex, pool.feeGrowthGlobalX, pool.feeGrowthGlobalY, pool.startTimestamp)
            flip(true, lowerTickIndex, poolKey)
        }

        if (!containsTick(poolKey, upperTickIndex)) {
            createTick{caller -> ALPH: mapEntry2Deposits}(caller, poolKey, upperTickIndex, pool.currentTickIndex, pool.feeGrowthGlobalX, pool.feeGrowthGlobalY, pool.startTimestamp)
            flip(true, upperTickIndex, poolKey)
        }

        assert!(pool.sqrtPrice >= slippageLimitLower && pool.sqrtPrice <= slippageLimitUpper, InvariantError.PriceLimitReached)

        let (_, lowerTick) = wrappedGetTick(poolKey, lowerTickIndex)
        let (_, upperTick) = wrappedGetTick(poolKey, upperTickIndex)

        let emptyPosition = Position {
          poolKey: poolKey,
          liquidity: 0,
          lowerTickIndex: lowerTickIndex,
          upperTickIndex: upperTickIndex,
          feeGrowthInsideX: 0,
          feeGrowthInsideY: 0,
          lastBlockNumber: currentTimestamp,
          tokensOwedX: 0,
          tokensOwedY: 0,
          owner: caller
        } 

        let (x, y, mPosition, _, _) = modify(emptyPosition, pool, lowerTick, upperTick, liquidityDelta, true, currentTimestamp)
        wrappedAddPosition{caller -> ALPH: mapEntry2Deposits}(caller, caller, mPosition)

        transferTokenToSelf!(callerAddress!(), pool.poolKey.tokenX, x)
        transferTokenToSelf!(callerAddress!(), pool.poolKey.tokenY, y)
    }

    @using(assetsInContract = true)
    pub fn removePosition(index: U256) -> () {
        let caller = callerAddress!()

        let (exist, position) = wrappedGetPosition(caller, index)

        assert!(exist, InvariantError.PositionNotFound)
        checkCaller!(caller == position.owner, InvariantError.NotOwner)

        let poolKey = position.poolKey
        let (_, upperTick) = wrappedGetTick(poolKey, position.upperTickIndex)
        let (_, lowerTick) = wrappedGetTick(poolKey, position.lowerTickIndex)

        let (_, pool) = wrappedGetPool(poolKey)

        let (mut x, mut y, mPosition, isLowerEmpty, isUpperEmpty) = modify(position, pool, lowerTick, upperTick, position.liquidity, false, blockTimeStamp!())
        
        x = x + mPosition.tokensOwedX
        y = y + mPosition.tokensOwedY

        if (isLowerEmpty) {
            removeTick(caller, poolKey, position.lowerTickIndex)
            flip(false, position.lowerTickIndex, poolKey)
        }
        if (isUpperEmpty) {
            removeTick(caller, poolKey, position.upperTickIndex)
            flip(false, position.upperTickIndex, poolKey)
        } 

        transferTokenFromSelf!(caller, pool.poolKey.tokenX, x)
        transferTokenFromSelf!(caller, pool.poolKey.tokenY, y)

        wrappedRemovePosition(caller, index)
    }

    @using(preapprovedAssets = true)
    pub fn transferPosition(index: U256, newOwner: Address) -> () {
        let caller = callerAddress!()
        let (exist, position) = wrappedGetPosition(caller, index)
        assert!(exist, InvariantError.PositionNotFound)
        checkCaller!(caller == position.owner, InvariantError.NotOwner)
        wrappedTransferPosition{caller -> ALPH: 2 * mapEntryDeposit!()}(caller, index, newOwner)
    }

    @using(checkExternalCaller = false)
    pub fn quote(
        poolKey: PoolKey,
        xToY: Bool,
        amount: U256,
        byAmountIn: Bool,
        sqrtPriceLimit: U256
    ) -> QuoteResult {
        let calculateSwapResult = calculateSwap(poolKey, xToY, amount, byAmountIn, sqrtPriceLimit)

        return QuoteResult { 
            amountIn: calculateSwapResult.amountIn,
            amountOut: calculateSwapResult.amountOut,
            targetSqrtPrice: calculateSwapResult.targetSqrtPrice
        }
    }

    fn calculateSwap(
        poolKey: PoolKey,
        xToY: Bool,
        amount: U256,
        byAmountIn: Bool,
        sqrtPriceLimit: U256
    ) -> CalculateSwapResult {
        assert!(amount != 0, InvariantError.ZeroAmount)
        let (exist, mut pool) = wrappedGetPool(poolKey)
        assert!(exist, InvariantError.PoolDoesNotExist)
        
        let mut sqrtPrice = pool.sqrtPrice
        let tickSpacing = poolKey.feeTier.tickSpacing

        if (xToY) {
            assert!(sqrtPrice > sqrtPriceLimit && sqrtPriceLimit <= MaxSqrtPrice, InvariantError.WrongPriceLimit)             
        } else {
            assert!(sqrtPrice < sqrtPriceLimit && sqrtPriceLimit >= MinSqrtPrice, InvariantError.WrongPriceLimit)
        }

        let tickLimit = if (xToY) getMinTick(tickSpacing) else getMaxTick(tickSpacing)

        let mut totalAmountIn = 0
        let mut totalAmountOut = 0
        let mut eventFeeAmount = 0
        let mut remainingAmount = amount
        let eventStartSqrtPrice = sqrtPrice

        while (remainingAmount != 0) {
            let currentTickIndex = pool.currentTickIndex
            let liquidity = pool.liquidity
            sqrtPrice = pool.sqrtPrice

            let (swapLimit, hasLimitingTick, limitingTickIndex, isInitialized) = getCloserLimit(
                sqrtPriceLimit,
                xToY,
                currentTickIndex,
                tickSpacing,
                poolKey
            )

            let swapResult = clamm.computeSwapStep(
                sqrtPrice,
                swapLimit,
                liquidity,
                remainingAmount,
                byAmountIn,
                poolKey.feeTier.fee
            )

            if (byAmountIn) {
                remainingAmount = remainingAmount - (swapResult.amountIn + swapResult.feeAmount)
            } else {
                remainingAmount = remainingAmount - swapResult.amountOut
            }

            pool = addFee(pool, swapResult.feeAmount, xToY, config.protocolFee)
            pool.sqrtPrice = swapResult.nextSqrtPrice

            totalAmountIn = totalAmountIn + swapResult.amountIn + swapResult.feeAmount
            totalAmountOut = totalAmountOut + swapResult.amountOut
            eventFeeAmount = eventFeeAmount + swapResult.feeAmount

            assert!(swapResult.nextSqrtPrice != sqrtPriceLimit || remainingAmount == 0, InvariantError.PriceLimitReached)

            let (amountToAdd, amountAfterTickUpdate, _, uPool) = poolUpdateTick(pool, swapResult.nextSqrtPrice, swapLimit, remainingAmount, byAmountIn, xToY, config.protocolFee, hasLimitingTick, isInitialized, limitingTickIndex)

            remainingAmount = amountAfterTickUpdate
            totalAmountIn = totalAmountIn + amountToAdd
            pool = uPool
            let reachedTickLimit = if (xToY) pool.currentTickIndex <= tickLimit else pool.currentTickIndex >= tickLimit

            assert!(!reachedTickLimit, InvariantError.TickLimitReached)
        }

        pool.lastTimestamp = blockTimeStamp!()
        let key = poolKeyBytes(poolKey)
        pools[key] = pool

        assert!(totalAmountOut != 0, InvariantError.NoGainSwap)
        
        return CalculateSwapResult {
            amountIn: totalAmountIn,
            amountOut: totalAmountOut,
            startSqrtPrice: eventStartSqrtPrice,
            targetSqrtPrice: pool.sqrtPrice,
            fee: eventFeeAmount
        }
    }

    @using(assetsInContract = true, preapprovedAssets = true, checkExternalCaller = false)
    pub fn swap(
        poolKey: PoolKey,
        xToY: Bool,
        amount: U256,
        byAmountIn: Bool,
        sqrtPriceLimit: U256
    ) -> CalculateSwapResult {
        let caller = callerAddress!()
        let calculateSwapResult = calculateSwap(poolKey, xToY, amount, byAmountIn, sqrtPriceLimit)

        let (_, pool) = wrappedGetPool(poolKey)
        let tokenX = pool.poolKey.tokenX
        let tokenY = pool.poolKey.tokenY

        if (xToY) {
            transferTokenToSelf!(caller, tokenX, calculateSwapResult.amountIn)
            transferTokenFromSelf!(caller, tokenY, calculateSwapResult.amountOut)
        } else {
            transferTokenFromSelf!(caller, tokenX, calculateSwapResult.amountOut)
            transferTokenToSelf!(caller, tokenY, calculateSwapResult.amountIn)
        }

        return calculateSwapResult
    }
    
    // caller is checked in `wrappedGetPosition`
    @using(assetsInContract = true, checkExternalCaller = false)
    pub fn claimFee(index: U256) -> () {
        let caller = callerAddress!()
        let key = toByteVec!(caller) ++ toByteVec!(index)
        let (exist, position) = wrappedGetPosition(caller, index)
        assert!(exist, InvariantError.PositionDoesNotExist)
    
        let poolKey = position.poolKey
        let (_, pool) = wrappedGetPool(poolKey)
        let (_, upperTick) = wrappedGetTick(poolKey, position.upperTickIndex)
        let (_, lowerTick) = wrappedGetTick(poolKey, position.lowerTickIndex)

        let (_, _, mut mPosition, _, _) = modify(position, pool, lowerTick, upperTick, 0, false, blockTimeStamp!())
        
        let tokensOwedX = mPosition.tokensOwedX
        let tokensOwedY = mPosition.tokensOwedY

        mPosition.tokensOwedX = 0
        mPosition.tokensOwedY = 0

        positions[key] = mPosition

        transferTokenFromSelf!(caller, pool.poolKey.tokenX, tokensOwedX)
        transferTokenFromSelf!(caller, pool.poolKey.tokenY, tokensOwedY)
    }
}
