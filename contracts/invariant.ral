Contract Invariant(
    mut init: Bool,
    admin: Address,
    mut protocolFee: U256,
    feeTiersContractId: ByteVec,
    poolKeysContractId: ByteVec, 
    poolsContractId: ByteVec, 
    ticksContractId: ByteVec, 
    positionsContractId: ByteVec,
    tickmapContractId: ByteVec,
    swapContractId: ByteVec,
    clammContractId: ByteVec
) extends Utils(), Decimal() {
    enum InvariantError {
        NotAdmin = 0
        AlreadyInitialized = 1
        NotInitialized = 2
        InvalidTickSpacing = 3
        InvalidFee = 4
        FeeTierNotFound = 5
        TokensAreSame = 6
        PoolKeyAlreadyExist = 7
        TickAndSqrtPriceMismatch = 8
        NotFeeReceiver = 9
        ZeroLiquidity = 10
        PriceLimitReached = 11
    }

    @using(preapprovedAssets = true, updateFields = true, checkExternalCaller = false)
    pub fn init() -> () {
        assert!(callerAddress!() == admin, InvariantError.NotAdmin)
        assert!(!init, InvariantError.AlreadyInitialized)

        init = true
    }
    
    pub fn getProtocolFee() -> U256 {
        return protocolFee
    }

    @using(updateFields = true, checkExternalCaller = false)
    pub fn changeProtocolFee(newProtocolFee: U256) -> () {
        assert!(callerAddress!() == admin, InvariantError.NotAdmin)
        protocolFee = newProtocolFee
    }

    @using(checkExternalCaller = false)
    pub fn withdrawProtocolFee(token0: ByteVec, token1: ByteVec, fee: U256, tickSpacing: U256) -> () {
        let caller = callerAddress!()
        let poolKey = generatePoolKey(token0, token1, fee, tickSpacing)
        let pool = Pool(poolKey)

        assert!(pool.getFeeReceiver() == caller, InvariantError.NotFeeReceiver)

        let (feeProtocolTokenX, feeProtocolTokenY) = pool.withdrawProtocolFee()

        transferTokenFromSelf!(caller, token0, feeProtocolTokenX)
        transferTokenFromSelf!(caller, token1, feeProtocolTokenY)
    }

    @using(checkExternalCaller = false)
    pub fn changeFeeReceiver(token0: ByteVec, token1: ByteVec, fee: U256, tickSpacing: U256, newFeeReceiver: Address) -> () {
        let caller = callerAddress!()
        let poolKey = generatePoolKey(token0, token1, fee, tickSpacing)
        let pool = Pool(poolKey)

        assert!(admin == caller, InvariantError.NotFeeReceiver)

        pool.setFeeReceiver(newFeeReceiver)
    }

    @using(preapprovedAssets = true, checkExternalCaller = false)
    pub fn createPool(token0: ByteVec, token1: ByteVec, fee: U256, tickSpacing: U256, initSqrtPrice: U256, initTick: I256) -> () {
        assert!(init, InvariantError.NotInitialized)

        let caller = callerAddress!()
        let contractAddress = selfAddress!()
        let timestamp = blockTimeStamp!()

        assert!(FeeTiers(feeTiersContractId).contains(fee, tickSpacing), InvariantError.FeeTierNotFound)

        CLAMM(clammContractId).checkTick(initTick, tickSpacing)

        assert!(token0 != token1, InvariantError.TokensAreSame)

        let poolKey = generatePoolKey(token0, token1, fee, tickSpacing)
        let (tokenX, tokenY) = extractTokensFromPoolKey(poolKey)
        
        let poolKeyExist = PoolKeys(poolKeysContractId).contains(tokenX, tokenY, fee, tickSpacing)
        assert!(!poolKeyExist, InvariantError.PoolKeyAlreadyExist)

        assert!(CLAMM(clammContractId).checkTickToSqrtPriceRelationship(initTick, tickSpacing, initSqrtPrice), InvariantError.TickAndSqrtPriceMismatch)

        PoolKeys(poolKeysContractId).add{caller -> ALPH: 1 alph}(caller, tokenX, tokenY, fee, tickSpacing)
        Pools(poolsContractId).add{caller -> ALPH: 1 alph}(caller, poolKey, tokenX, tokenY, tickSpacing, initSqrtPrice, initTick, timestamp, contractAddress)
    }

    @using(preapprovedAssets = true, checkExternalCaller = false)
    pub fn addFeeTier(fee: U256, tickSpacing: U256) -> () {
        assert!(init, InvariantError.NotInitialized)

        let caller = callerAddress!()

        assert!(tickSpacing > 0 || tickSpacing <= 100, InvariantError.InvalidTickSpacing)
        assert!(fee <= 1000000000000, InvariantError.InvalidFee)
        assert!(caller == admin, InvariantError.NotAdmin)

        FeeTiers(feeTiersContractId).add{caller -> ALPH: 1 alph}(caller, fee, tickSpacing)
    }

    @using(checkExternalCaller = false)
    pub fn removeFeeTier(fee: U256, tickSpacing: U256) -> () {
        assert!(init, InvariantError.NotInitialized)
        assert!(callerAddress!() == admin, InvariantError.NotAdmin)

        FeeTiers(feeTiersContractId).remove(fee, tickSpacing)
    }

    pub fn feeTierExist(fee: U256, tickSpacing: U256) -> Bool {
        return FeeTiers(feeTiersContractId).contains(fee, tickSpacing)
    }

    pub fn getFeeTiers() -> ByteVec {
        return FeeTiers(feeTiersContractId).getAll()
    }

    pub fn getPools() -> ByteVec {
        return PoolKeys(poolKeysContractId).getAll()
    }

    pub fn getTick(token0: ByteVec, token1: ByteVec, fee: U256, tickSpacing: U256, index: I256) -> (Bool, Bool, U256, U256, U256, U256, U256, U256) {
        return Ticks(ticksContractId).get(generatePoolKey(token0, token1, fee, tickSpacing), index)
    }

    pub fn getPool(token0: ByteVec, token1: ByteVec, fee: U256, tickSpacing: U256) -> (Bool, U256, U256, I256, U256, U256, U256, U256, U256, U256, Address) {
        return Pools(poolsContractId).get(generatePoolKey(token0, token1, fee, tickSpacing))
    }

    pub fn getPosition(index: U256) -> (Bool, U256, I256, I256, U256, U256, U256, U256, U256) {
        return Positions(positionsContractId).get(index)
    }

    pub fn isTickInitialized(token0: ByteVec, token1: ByteVec, fee: U256, tickSpacing: U256, index: I256) -> Bool {
        let poolKey = generatePoolKey(token0, token1, fee, tickSpacing)

        return Tickmap(tickmapContractId).get(index, tickSpacing, poolKey)
    }

    // Can be moved to collection layer
    @using(preapprovedAssets = true, checkExternalCaller = false)
    pub fn createTick(
        caller: Address,
        poolKey: ByteVec,
        tickSpacing: U256,
        index: I256,
        currentTickIndex: I256,
        feeGrowthGlobalX: U256,
        feeGrowthGlobalY: U256,
        startTimestamp: U256
    ) -> () {
        CLAMM(clammContractId).checkTick(index, tickSpacing)

        let mut feeGrowthOutsideX = 0
        let mut feeGrowthOutsideY = 0
        let mut secondsOutside = 0

        if (index <= currentTickIndex) {
            feeGrowthOutsideX = feeGrowthGlobalX
            feeGrowthOutsideY = feeGrowthGlobalY
            secondsOutside = blockTimeStamp!() - startTimestamp
        }

        Ticks(ticksContractId).add{caller -> ALPH: 1 alph}(
            caller,
            poolKey,
            index,
            true,
            0,
            0,
            CLAMM(clammContractId).calculateSqrtPrice(index),
            feeGrowthOutsideX,
            feeGrowthOutsideY, 
            0
        )
    }

    @using(preapprovedAssets = true, checkExternalCaller = false)
    pub fn initPosition(
        token0: ByteVec,
        token1: ByteVec,
        fee: U256,
        tickSpacing: U256,
        lowerTick: I256,
        upperTick: I256
    ) -> () {
        let pools = Pools(poolsContractId) 
        let poolKey = generatePoolKey(token0, token1, fee, tickSpacing)

        let currentTickindex = pools.getCurrentTickIndex(poolKey)
        let (feeGrowthGlobalX, feeGrowthGlobalY) = pools.getFeeGrowthGlobal(poolKey)
        let startTimestamp = pools.getStartTimestamp(poolKey)

        let ticks = Ticks(ticksContractId)
        let caller = callerAddress!()

        let (lowerChunk, upperChunk) = Tickmap(tickmapContractId).getChunks(lowerTick, upperTick, tickSpacing)

        if (lowerChunk == upperChunk) {
            Tickmap(tickmapContractId).initializeChunk{caller -> ALPH: 1 alph}(caller, poolKey, lowerChunk)
        } else {
            Tickmap(tickmapContractId).initializeChunk{caller -> ALPH: 1 alph}(caller, poolKey, lowerChunk)
            Tickmap(tickmapContractId).initializeChunk{caller -> ALPH: 1 alph}(caller, poolKey, upperChunk)
        }
 
        if(!ticks.exist(poolKey, lowerTick)) {
            createTick{caller -> ALPH: 2 alph}(caller, poolKey, tickSpacing, lowerTick, currentTickindex, feeGrowthGlobalX, feeGrowthGlobalY, startTimestamp)
        }

        if(!ticks.exist(poolKey, upperTick)) {
            createTick{caller -> ALPH: 2 alph}(caller, poolKey, tickSpacing, upperTick, currentTickindex, feeGrowthGlobalX, feeGrowthGlobalY, startTimestamp)
        }

        Positions(positionsContractId).add{caller -> ALPH: 2 alph}(caller, poolKey, 0, lowerTick, upperTick, 0, 0, blockTimeStamp!(), 0, 0)
    }

    @using(preapprovedAssets = true, assetsInContract = true, checkExternalCaller = false)
    pub fn createPosition(
        index: U256,
        token0: ByteVec,
        token1: ByteVec,
        fee: U256,
        tickSpacing: U256,
        lowerTick: I256,
        upperTick: I256,
        liquidityDelta: U256,
        slippageLimitLower: U256,
        slippageLimitUpper: U256
    ) -> () {
        let pools = Pools(poolsContractId)
        let currentTimestamp = blockTimeStamp!()

        assert!(liquidityDelta != 0, InvariantError.ZeroLiquidity)

        let poolKey = generatePoolKey(token0, token1, fee, tickSpacing)

        if(isTickInitialized(token0, token1, fee, tickSpacing, lowerTick) == false) {
            Tickmap(tickmapContractId).flip(true, lowerTick, tickSpacing, poolKey)
        }
        
        if(isTickInitialized(token0, token1, fee, tickSpacing, upperTick) == false) {
            Tickmap(tickmapContractId).flip(true, upperTick, tickSpacing, poolKey)
        }

        let poolCurrentSqrtPrice = pools.getCurrentSqrtPrice(poolKey)

        assert!(poolCurrentSqrtPrice >= slippageLimitLower && poolCurrentSqrtPrice <= slippageLimitUpper, InvariantError.PriceLimitReached)

        let (x, y) = Positions(positionsContractId).wrappedModify(index, poolsContractId, ticksContractId, clammContractId,  poolKey, upperTick, lowerTick, liquidityDelta, true, currentTimestamp, tickSpacing)

        let tokenX = pools.getTokenX(poolKey)
        let tokenY = pools.getTokenY(poolKey)

        transferTokenToSelf!(callerAddress!(), tokenX, x)
        transferTokenToSelf!(callerAddress!(), tokenY, y)
    }

    @using(assetsInContract = true, checkExternalCaller = false)
    pub fn removePosition(index: U256) -> () {
        let caller = callerAddress!()
        let positions = Positions(positionsContractId)

        let poolKey = positions.getPoolKey(index)
        let upperTick = positions.getUpperTickIndex(index)
        let lowerTick = positions.getLowerTickIndex(index)
        let liquidity = positions.getLiquidity(index)

        let tickSpacing = Pools(poolsContractId).getTickSpacing(poolKey)

        let (mut x, mut y) = Positions(positionsContractId).wrappedModify(index, poolsContractId, ticksContractId, clammContractId,  poolKey, upperTick, lowerTick, liquidity, false, blockTimeStamp!(), tickSpacing)

        let pools = Pools(poolsContractId)

        let tokenX = pools.getTokenX(poolKey)
        let tokenY = pools.getTokenY(poolKey)
        let tokensOwedX = positions.getTokensOwedX(index)
        let tokensOwedY = positions.getTokensOwedY(index)

        positions.remove(caller, index)

        x = x + tokensOwedX
        y = y + tokensOwedY

        transferTokenFromSelf!(caller, tokenX, x)
        transferTokenFromSelf!(caller, tokenY, y)
    }

    @using(checkExternalCaller = false)
    pub fn transferPosition(index: U256, newOwner: Address) -> () {
        Positions(positionsContractId).transfer(callerAddress!(), index, newOwner)
    }

    @using(updateFields = false, checkExternalCaller = false)
    pub fn quote(
        token0: ByteVec,
        token1: ByteVec,
        fee: U256,
        tickSpacing: U256,
        xToY: Bool,
        amount: U256,
        byAmountIn: Bool,
        sqrtPriceLimit: U256
    ) -> (U256, U256, U256) {
        return SwapUtils(swapContractId).wrappedQuote(token0, token1, fee, tickSpacing, xToY, amount, byAmountIn, sqrtPriceLimit)
    }

    @using(assetsInContract = true, preapprovedAssets = true, checkExternalCaller = false)
    pub fn swap(
        token0: ByteVec,
        token1: ByteVec,
        fee: U256,
        tickSpacing: U256,
        xToY: Bool,
        amount: U256,
        byAmountIn: Bool,
        sqrtPriceLimit: U256
    ) -> (U256, U256, U256, U256, U256) {
        let caller = callerAddress!()
        let poolKey = generatePoolKey(token0, token1, fee, tickSpacing)

        let (amountIn, amountOut, startingSqrtPrice, targetSqrtPrice, feeAmount) = SwapUtils(swapContractId).wrappedSwap(token0, token1, fee, tickSpacing, xToY, amount, byAmountIn, sqrtPriceLimit)

        let tokenXId = Pools(poolsContractId).getTokenX(poolKey)
        let tokenYId = Pools(poolsContractId).getTokenY(poolKey)

        if (xToY) {
            transferTokenToSelf!(caller, tokenXId, amountIn)
            transferTokenFromSelf!(caller, tokenYId, amountOut)
        } else {
            transferTokenFromSelf!(caller, tokenXId, amountOut)
            transferTokenToSelf!(caller, tokenYId, amountIn)
        }

        return amountIn, amountOut, startingSqrtPrice, targetSqrtPrice, feeAmount
    }
    
    @using(assetsInContract = true, checkExternalCaller = false)
    pub fn claimFee(index: U256) -> () {
        let caller = callerAddress!()
        let positions = Positions(positionsContractId)
        let pools = Pools(poolsContractId)

        let poolKey = positions.getPoolKey(index)
        let upperTick = positions.getUpperTickIndex(index)
        let lowerTick = positions.getLowerTickIndex(index)
        let tickSpacing = pools.getTickSpacing(poolKey)

        positions.wrappedModify(index, poolsContractId, ticksContractId, clammContractId,  poolKey, upperTick, lowerTick, 0, false, blockTimeStamp!(), tickSpacing)
        
        let tokensOwedX = positions.getTokensOwedX(index)
        let tokensOwedY = positions.getTokensOwedY(index)

        positions.setTokensOwedX(index, 0)
        positions.setTokensOwedY(index, 0)

        let tokenX = pools.getTokenX(poolKey)
        let tokenY = pools.getTokenY(poolKey)

        transferTokenFromSelf!(caller, tokenX, tokensOwedX)
        transferTokenFromSelf!(caller, tokenY, tokensOwedY)
    }
}
