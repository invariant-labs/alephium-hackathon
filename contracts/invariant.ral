Contract Invariant(
    admin: Address,
    protocolFee: U256,
    feeTierTemplateContractId: ByteVec,
    mut feeTierCount: U256,
    poolKeyTemplateContractId: ByteVec,
    mut poolKeyCount: U256,
    poolTemplateContractId: ByteVec,
    tickTemplateContractId: ByteVec
) extends FeeTiers(feeTierTemplateContractId, feeTierCount), PoolKeys(poolKeyTemplateContractId, poolKeyCount), Pools(poolTemplateContractId), CLAMM(), CollectionsPrefixes() {
    pub fn getProtocolFee() -> U256 {
        return protocolFee
    }

    @using(preapprovedAssets = true, checkExternalCaller = false)
    pub fn createPool(token0: Address, token1: Address, fee: U256, tickSpacing: U256, initSqrtPrice: U256, initTick: I256) -> () {
        let caller = callerAddress!()
        let contractAddress = selfAddress!()
        let timestamp = blockTimeStamp!()

        assert!(feeTiersContains(fee, tickSpacing), InvariantError.FeeTierNotFound)

        checkTick(initTick, tickSpacing)

        assert!(token0 != token1, InvariantError.TokensAreSame)

        let poolKeyExist = poolKeysContains(token0, token1, fee, tickSpacing)

        if (!poolKeyExist) {
            let swappedPoolKeyExist = poolKeysContains(token0, token1, fee, tickSpacing)

            assert!(!swappedPoolKeyExist, InvariantError.PoolKeyAlreadyExist)
        }

        checkTickToSqrtPriceRelationship(initTick, tickSpacing, initSqrtPrice)

        poolKeysAdd{caller -> ALPH: 1 alph}(caller, token0, token1, fee, tickSpacing)
        poolsAdd{caller -> ALPH: 1 alph}(caller, token0, token1, fee, tickSpacing, initSqrtPrice, initTick, timestamp, contractAddress)
    }

    @using(preapprovedAssets = true, checkExternalCaller = false)
    pub fn addFeeTier(fee: U256, tickSpacing: U256) -> () {
        let caller = callerAddress!()

        assert!(tickSpacing > 0 || tickSpacing <= 100, InvariantError.InvalidTickSpacing)
        assert!(fee <= 1000000000000, InvariantError.InvalidFee)
        assert!(caller == admin, InvariantError.NotAdmin)

        feeTiersAdd{caller -> ALPH: 1 alph}(caller, fee, tickSpacing)
    }

    @using(checkExternalCaller = false)
    pub fn removeFeeTier(fee: U256, tickSpacing: U256) -> () {
        assert!(callerAddress!() == admin, InvariantError.NotAdmin)

        feeTiersRemove(fee, tickSpacing)
    }

    pub fn feeTierExist(fee: U256, tickSpacing: U256) -> Bool {
        return feeTiersContains(fee, tickSpacing)
    }

    // TODO: remove if possible after implementing get fee tiers
    pub fn getFeeTierCount() -> U256 {
        let mut count = 0

        for (let mut index = 1; index <= feeTierCount; index = index + 1) {
            let contract = FeeTier(subContractId!(toByteVec!(InvariantCollection.FeeTiers) ++ toByteVec!(index)))

            if (contract.isActive()) {
                count = count + 1
            }
        }

        return count
    }

    // TODO: Should not be pub
    @using(checkExternalCaller = false)
    pub fn createTick(poolKey: ByteVec, tickSpacing: U256, index: I256, poolCurrentIndex: I256, poolFeeGrowthGlobalX: U256, poolFeeGrowthGlobalY: U256, poolStartTimestamp: U256) -> () {
        let currentTimestamp = blockTimeStamp!()
        let caller = callerAddress!()
        let belowCurrentTick = index <= poolCurrentIndex

        checkTick(index, tickSpacing)

        
        let mut feeGrowthOutsideX = 0
        let mut feeGrowthOutisdeY = 0
        let mut secondsOutside = 0
        if (belowCurrentTick) {
            feeGrowthOutsideX = poolFeeGrowthGlobalX
            feeGrowthOutisdeY = poolFeeGrowthGlobalY
            secondsOutside = currentTimestamp - poolStartTimestamp
        }

        let tickKey = toByteVec!(InvariantCollection.Ticks) ++ poolKey ++ toByteVec!(index)
        let (encodedImmFields, encodedMutFields) = Tick.encodeFields!(index, true, 0, 0, sqrtPriceFromTick(index), feeGrowthOutsideX, feeGrowthOutisdeY, secondsOutside)
        copyCreateSubContract!{caller -> ALPH: 1 alph}(
            tickKey,
            tickTemplateContractId,
            encodedImmFields,
            encodedMutFields
        )       
        // Flip bit in tickmap
    }

    // TODO: implement
    pub fn getFeeTiers() -> () {}
}
