Contract Invariant(
    mut config: InvariantConfig,
    clamm: CLAMM,
    mut feeTiers: FeeTiers,
    reserveTemplateId: ByteVec,
    mut lastReserveId: ByteVec,
    mut feeTierCount: U256,
    mut poolKeyCount: U256
) extends PoolKeyHelper(), Decimal(), PoolKeys(), Pools(clamm), Ticks(), Tickmap(), Positions(clamm), FeeTierHelper(), Reserves() {
    // Key: poolKeyCount
    mapping[U256, PoolKey] poolKeys
    // Key: poolKey
    mapping[ByteVec, Pool] pools
    // Key: poolKey ++ index
    mapping[ByteVec, Tick] ticks
    // Key: poolKey ++ subcontract index
    mapping[ByteVec, TickmapBatch] bitmap
    // Key: owner ++ index
    mapping[ByteVec, Position] positions
    // Key: owner
    mapping[Address, U256] positionsCounter
    // Key: TokenId, Value: ReserveId
    mapping[ByteVec, ByteVec] reserves

    
    pub fn getProtocolFee() -> Percentage {
        return config.protocolFee
    }

    @using(updateFields = true)
    pub fn changeProtocolFee(newProtocolFee: Percentage) -> () {
        checkCaller!(callerAddress!() == config.admin, InvariantError.NotAdmin)
        assert!(newProtocolFee.v < PERCENTAGE_DENOMINATOR, InvariantError.InvalidProtocolFee)
        config.protocolFee = newProtocolFee
    }

    pub fn withdrawProtocolFee(poolKey: PoolKey) -> () {
        let caller = callerAddress!()
        let (exist, mut pool) = wrappedGetPool(poolKey)
        assert!(exist, InvariantError.PoolNotFound)

        let feeReceiver = pool.feeReceiver

        checkCaller!(feeReceiver == caller, InvariantError.NotFeeReceiver)

        let x = pool.feeProtocolTokenX
        let y = pool.feeProtocolTokenY
        pool.feeProtocolTokenX = TokenAmount{v: 0}
        pool.feeProtocolTokenY = TokenAmount{v: 0}

        rewritePool(pool)

        if (pool.reserveX == pool.reserveY) {
            Reserve(pool.reserveX).withdrawTwoAssets(caller, pool.poolKey.tokenX, pool.poolKey.tokenY, x, y)
        } else {
            Reserve(pool.reserveX).withdrawSingleAsset(caller, pool.poolKey.tokenX, x)
            Reserve(pool.reserveY).withdrawSingleAsset(caller, pool.poolKey.tokenY, y)    
        }
    }

    pub fn changeFeeReceiver(poolKey: PoolKey, newFeeReceiver: Address) -> () {
        let caller = callerAddress!()

        checkCaller!(config.admin == caller, InvariantError.NotAdmin)

        let key = poolKeyBytes(poolKey)
        let mut pool = pools[key]

        pool.feeReceiver = newFeeReceiver
        pools[key] = pool
    }

    @using(preapprovedAssets = true, checkExternalCaller = false)
    pub fn createPool(token0: ByteVec, token1: ByteVec, feeTier: FeeTier, initSqrtPrice: SqrtPrice, initTick: I256) -> () {
        assert!(feeTierExist(feeTier), InvariantError.FeeTierNotFound)

        clamm.checkTick(initTick, feeTier.tickSpacing)

        assert!(token0 != token1, InvariantError.TokensAreSame)

        assert!(clamm.checkTickToSqrtPriceRelationship(initTick, feeTier.tickSpacing, initSqrtPrice), InvariantError.TickAndSqrtPriceMismatch)

        let caller = callerAddress!()
        let timestamp = blockTimeStamp!()
        let mapEntryDeposit = mapEntryDeposit!()
        let poolKey = newPoolKey(token0, token1, feeTier)
        addPoolKey{caller -> ALPH: mapEntryDeposit}(caller, poolKey)
        addPool{caller -> ALPH: mapEntryDeposit * 4}(caller, poolKey, initSqrtPrice, initTick, timestamp, config.admin)
    }

    @using(preapprovedAssets = true, updateFields = true)
    pub fn addFeeTier(feeTier: FeeTier) -> () {
        let caller = callerAddress!()
        checkCaller!(caller == config.admin, InvariantError.NotAdmin)

        assert!(feeTierCount < MAX_FEE_TIERS, InvariantError.FeeTierLimitReached)
        assert!(feeTier.tickSpacing > 0 && feeTier.tickSpacing <= 100, InvariantError.InvalidTickSpacing)
        assert!(feeTier.fee.v < PERCENTAGE_DENOMINATOR, InvariantError.InvalidFee)
        assert!(!feeTierExist(feeTier), InvariantError.FeeTierAlreadyExist)
        
        feeTiers.feeTiers[feeTierCount] = feeTier
        feeTierCount = feeTierCount + 1
    }

    @using(updateFields = true)
    pub fn removeFeeTier(feeTier: FeeTier) -> () {
        let caller = callerAddress!()
        checkCaller!(caller == config.admin, InvariantError.NotAdmin)
        
        for(let mut i = 0; i < feeTierCount; i = i + 1) {
            if(feeTiers.feeTiers[i].fee.v == feeTier.fee.v && feeTiers.feeTiers[i].tickSpacing == feeTier.tickSpacing) {
                feeTiers.feeTiers[i] = feeTiers.feeTiers[feeTierCount - 1]
                feeTiers.feeTiers[feeTierCount - 1] = FeeTier{fee: Percentage{v: 0}, tickSpacing: 0}
                feeTierCount = feeTierCount - 1
                return
            }
        }
        assert!(false, InvariantError.FeeTierNotFound)
    }

    pub fn feeTierExist(feeTier: FeeTier) -> Bool {
        for(let mut i = 0; i < feeTierCount; i = i + 1) {
            if(feeTiers.feeTiers[i].fee.v == feeTier.fee.v && feeTiers.feeTiers[i].tickSpacing == feeTier.tickSpacing) {
                return true
            }
        }
        return false
    }

    pub fn getTick(poolKey: PoolKey, index: I256) -> (Bool, Tick) {
        return wrappedGetTick(poolKey, index)
    }

    pub fn getPool(poolKey: PoolKey) -> (Bool, Pool) {
        return wrappedGetPool(poolKey)
    }

    pub fn getPosition(owner: Address, index: U256) -> (Bool, Position) {  
        return wrappedGetPosition(owner, index)
    }

    pub fn isTickInitialized(poolKey: PoolKey, index: I256) -> Bool {
        return getBit(index, poolKey)    
    }

    @using(preapprovedAssets = true)
    fn createTick(
        caller: Address,
        poolKey: PoolKey,
        index: I256,
        currentTickIndex: I256,
        feeGrowthGlobalX: FeeGrowth,
        feeGrowthGlobalY: FeeGrowth,
        startTimestamp: U256
    ) -> () {
        clamm.checkTick(index, poolKey.feeTier.tickSpacing)

        let mut feeGrowthOutsideX = FeeGrowth{v: 0}
        let mut feeGrowthOutsideY = FeeGrowth{v: 0}
        let mut secondsOutside = 0

        if (index <= currentTickIndex) {
            feeGrowthOutsideX = feeGrowthGlobalX
            feeGrowthOutsideY = feeGrowthGlobalY
            secondsOutside = blockTimeStamp!() - startTimestamp
        }

        wrappedCreateTick{caller -> ALPH: mapEntryDeposit!()}(
            caller,
            poolKey,
            index,
            true,
            Liquidity{v: 0},
            Liquidity{v: 0},
            clamm.calculateSqrtPrice(index),
            feeGrowthOutsideX,
            feeGrowthOutsideY, 
            0
        )
    }

    @using(preapprovedAssets = true, checkExternalCaller = false)
    pub fn createPosition(
        poolKey: PoolKey,
        lowerTickIndex: I256,
        upperTickIndex: I256,
        liquidityDelta: Liquidity,
        slippageLimitLower: SqrtPrice,
        slippageLimitUpper: SqrtPrice
    ) -> () {
        let (exist, pool) = wrappedGetPool(poolKey)
        assert!(exist, InvariantError.PoolNotFound)
        assert!(liquidityDelta.v != 0, InvariantError.ZeroLiquidity)
        assert!(lowerTickIndex != upperTickIndex, InvariantError.InvalidTickIndex)

        let currentTimestamp = blockTimeStamp!()
        let mapEntryDeposit = mapEntryDeposit!()
        let mapEntry2Deposits = mapEntryDeposit * 2
        let caller = callerAddress!()

        let (lowerChunk, upperChunk) = getChunks(lowerTickIndex, upperTickIndex, poolKey.feeTier.tickSpacing)

        if (lowerChunk == upperChunk) {
            initializeChunk{caller -> ALPH: mapEntryDeposit}(caller, poolKey, lowerChunk)
        } else {
            initializeChunk{caller -> ALPH: mapEntryDeposit}(caller, poolKey, lowerChunk)
            initializeChunk{caller -> ALPH: mapEntryDeposit}(caller, poolKey, upperChunk)
        }
        
        if (!containsTick(poolKey, lowerTickIndex)) {
            createTick{caller -> ALPH: mapEntry2Deposits}(caller, poolKey, lowerTickIndex, pool.currentTickIndex, pool.feeGrowthGlobalX, pool.feeGrowthGlobalY, pool.startTimestamp)
            flip(true, lowerTickIndex, poolKey)
        }

        if (!containsTick(poolKey, upperTickIndex)) {
            createTick{caller -> ALPH: mapEntry2Deposits}(caller, poolKey, upperTickIndex, pool.currentTickIndex, pool.feeGrowthGlobalX, pool.feeGrowthGlobalY, pool.startTimestamp)
            flip(true, upperTickIndex, poolKey)
        }

        assert!(pool.sqrtPrice.v >= slippageLimitLower.v && pool.sqrtPrice.v <= slippageLimitUpper.v, InvariantError.PriceLimitReached)

        let (_, lowerTick) = wrappedGetTick(poolKey, lowerTickIndex)
        let (_, upperTick) = wrappedGetTick(poolKey, upperTickIndex)

        let emptyPosition = Position {
          poolKey: poolKey,
          liquidity: Liquidity{v: 0},
          lowerTickIndex: lowerTickIndex,
          upperTickIndex: upperTickIndex,
          feeGrowthInsideX: FeeGrowth{v: 0},
          feeGrowthInsideY: FeeGrowth{v: 0},
          lastBlockNumber: currentTimestamp,
          tokensOwedX: TokenAmount{v: 0},
          tokensOwedY: TokenAmount{v: 0},
          owner: caller
        }

        let (x, y, mPosition, _, _) = modify(emptyPosition, pool, lowerTick, upperTick, liquidityDelta, true, currentTimestamp)
        wrappedAddPosition{caller -> ALPH: mapEntry2Deposits}(caller, caller, mPosition)

        if (pool.reserveX == pool.reserveY) {
            Reserve(pool.reserveX).depositTwoAssets{caller -> pool.poolKey.tokenX: x.v, pool.poolKey.tokenY: y.v}(caller, pool.poolKey.tokenX, pool.poolKey.tokenY, x, y)
        } else {
            Reserve(pool.reserveX).depositSingleAsset{caller -> pool.poolKey.tokenX: x.v}(caller, pool.poolKey.tokenX, x)
            Reserve(pool.reserveY).depositSingleAsset{caller -> pool.poolKey.tokenY: y.v}(caller, pool.poolKey.tokenY, y)    
        }
    }
    @using(checkExternalCaller = false)
    pub fn removePosition(index: U256) -> () {
        let caller = callerAddress!()

        let (exist, position) = wrappedGetPosition(caller, index)
        assert!(exist, InvariantError.PositionNotFound)

        let poolKey = position.poolKey
        let (_, upperTick) = wrappedGetTick(poolKey, position.upperTickIndex)
        let (_, lowerTick) = wrappedGetTick(poolKey, position.lowerTickIndex)

        let (_, pool) = wrappedGetPool(poolKey)

        let (mut x, mut y, mPosition, isLowerEmpty, isUpperEmpty) = modify(position, pool, lowerTick, upperTick, position.liquidity, false, blockTimeStamp!())
        
        x.v = x.v + mPosition.tokensOwedX.v
        y.v = y.v + mPosition.tokensOwedY.v

        if (isLowerEmpty) {
            removeTick(caller, poolKey, position.lowerTickIndex)
            flip(false, position.lowerTickIndex, poolKey)
        }
        if (isUpperEmpty) {
            removeTick(caller, poolKey, position.upperTickIndex)
            flip(false, position.upperTickIndex, poolKey)
        } 

        if (pool.reserveX == pool.reserveY) {
            Reserve(pool.reserveX).withdrawTwoAssets(caller, pool.poolKey.tokenX, pool.poolKey.tokenY, x, y)
        } else {
            Reserve(pool.reserveX).withdrawSingleAsset(caller, pool.poolKey.tokenX, x)
            Reserve(pool.reserveY).withdrawSingleAsset(caller, pool.poolKey.tokenY, y)    
        }       

        wrappedRemovePosition(caller, index)
    }

    @using(preapprovedAssets = true, checkExternalCaller = false)
    pub fn transferPosition(index: U256, newOwner: Address) -> () {
        let caller = callerAddress!()
        assert!(index < positionCount(caller), InvariantError.PositionNotFound)

        wrappedTransferPosition{caller -> ALPH: 2 * mapEntryDeposit!()}(caller, index, newOwner)
    }

    @using(checkExternalCaller = false)
    pub fn quote(
        poolKey: PoolKey,
        xToY: Bool,
        amount: TokenAmount,
        byAmountIn: Bool,
        sqrtPriceLimit: SqrtPrice
    ) -> QuoteResult {
        let calculateSwapResult = calculateSwap(poolKey, xToY, amount, byAmountIn, sqrtPriceLimit)

        return QuoteResult { 
            amountIn: calculateSwapResult.amountIn,
            amountOut: calculateSwapResult.amountOut,
            targetSqrtPrice: calculateSwapResult.targetSqrtPrice
        }
    }

    fn calculateSwap(
        poolKey: PoolKey,
        xToY: Bool,
        amount: TokenAmount,
        byAmountIn: Bool,
        sqrtPriceLimit: SqrtPrice
    ) -> CalculateSwapResult {
        assert!(amount.v != 0, InvariantError.ZeroAmount)
        let (exist, mut pool) = wrappedGetPool(poolKey)
        assert!(exist, InvariantError.PoolNotFound)
        
        let mut sqrtPrice = pool.sqrtPrice
        let tickSpacing = poolKey.feeTier.tickSpacing

        if (xToY) {
            assert!(sqrtPrice.v > sqrtPriceLimit.v && sqrtPriceLimit.v <= MAX_SQRT_PRICE, InvariantError.WrongPriceLimit)             
        } else {
            assert!(sqrtPrice.v < sqrtPriceLimit.v && sqrtPriceLimit.v >= MIN_SQRT_PRICE, InvariantError.WrongPriceLimit)
        }

        let tickLimit = if (xToY) getMinTick(tickSpacing) else getMaxTick(tickSpacing)

        let mut totalAmountIn = TokenAmount{v: 0}
        let mut totalAmountOut = TokenAmount{v: 0}
        let mut eventFeeAmount = TokenAmount{v: 0}
        let mut remainingAmount = amount
        let eventStartSqrtPrice = sqrtPrice

        while (remainingAmount.v != 0) {
            let currentTickIndex = pool.currentTickIndex
            let liquidity = pool.liquidity
            sqrtPrice = pool.sqrtPrice

            let (swapLimit, hasLimitingTick, limitingTickIndex, isInitialized) = getCloserLimit(
                sqrtPriceLimit,
                xToY,
                currentTickIndex,
                tickSpacing,
                poolKey
            )

            let swapResult = clamm.computeSwapStep(
                sqrtPrice,
                swapLimit,
                liquidity,
                remainingAmount,
                byAmountIn,
                poolKey.feeTier.fee
            )

            if (byAmountIn) {
                remainingAmount.v = remainingAmount.v - (swapResult.amountIn.v + swapResult.feeAmount.v)
            } else {
                remainingAmount.v = remainingAmount.v - swapResult.amountOut.v
            }

            pool = addFee(pool, swapResult.feeAmount, xToY, config.protocolFee)
            pool.sqrtPrice = swapResult.nextSqrtPrice

            totalAmountIn.v = totalAmountIn.v + swapResult.amountIn.v + swapResult.feeAmount.v
            totalAmountOut.v = totalAmountOut.v + swapResult.amountOut.v
            eventFeeAmount.v = eventFeeAmount.v + swapResult.feeAmount.v

            assert!(swapResult.nextSqrtPrice.v != sqrtPriceLimit.v || remainingAmount.v == 0, InvariantError.PriceLimitReached)

            let (amountToAdd, amountAfterTickUpdate, _, uPool) = poolUpdateTick(pool, swapResult.nextSqrtPrice, swapLimit, remainingAmount, byAmountIn, xToY, config.protocolFee, hasLimitingTick, isInitialized, limitingTickIndex)

            remainingAmount = amountAfterTickUpdate
            totalAmountIn.v = totalAmountIn.v + amountToAdd.v
            pool = uPool
            let reachedTickLimit = if (xToY) pool.currentTickIndex <= tickLimit else pool.currentTickIndex >= tickLimit

            assert!(!reachedTickLimit, InvariantError.TickLimitReached)
        }

        pool.lastTimestamp = blockTimeStamp!()
        let key = poolKeyBytes(poolKey)
        pools[key] = pool

        assert!(totalAmountOut.v != 0, InvariantError.NoGainSwap)
        
        return CalculateSwapResult {
            amountIn: totalAmountIn,
            amountOut: totalAmountOut,
            startSqrtPrice: eventStartSqrtPrice,
            targetSqrtPrice: pool.sqrtPrice,
            fee: eventFeeAmount
        }
    }

    @using(preapprovedAssets = true, checkExternalCaller = false)
    pub fn swap(
        poolKey: PoolKey,
        xToY: Bool,
        amount: TokenAmount,
        byAmountIn: Bool,
        sqrtPriceLimit: SqrtPrice
    ) -> CalculateSwapResult {
        let caller = callerAddress!()
        let calculateSwapResult = calculateSwap(poolKey, xToY, amount, byAmountIn, sqrtPriceLimit)

        let (_, pool) = wrappedGetPool(poolKey)
        let tokenX = pool.poolKey.tokenX
        let tokenY = pool.poolKey.tokenY

        if (xToY) {
            if (pool.reserveX == pool.reserveY) {
                Reserve(pool.reserveX).swap{caller -> tokenX: calculateSwapResult.amountIn.v}(caller, tokenX, tokenY, calculateSwapResult.amountIn, calculateSwapResult.amountOut)
            } else {
                Reserve(pool.reserveX).depositSingleAsset{caller -> tokenX: calculateSwapResult.amountIn.v}(caller, tokenX, calculateSwapResult.amountIn)
                Reserve(pool.reserveY).withdrawSingleAsset(caller, tokenY, calculateSwapResult.amountOut)    
            }
        } else {
            if (pool.reserveX == pool.reserveY) {
                Reserve(pool.reserveX).swap{caller -> tokenY: calculateSwapResult.amountIn.v}(caller, tokenY, tokenX, calculateSwapResult.amountIn, calculateSwapResult.amountOut)
            } else {
                Reserve(pool.reserveX).depositSingleAsset{caller -> tokenY: calculateSwapResult.amountIn.v}(caller, tokenY, calculateSwapResult.amountIn)
                Reserve(pool.reserveY).withdrawSingleAsset(caller, tokenX, calculateSwapResult.amountOut)    
            }
        }

        return calculateSwapResult
    }
    
    // caller is checked in `wrappedGetPosition`
    @using(checkExternalCaller = false)
    pub fn claimFee(index: U256) -> () {
        let caller = callerAddress!()
        let key = toByteVec!(caller) ++ toByteVec!(index)
        let (exist, position) = wrappedGetPosition(caller, index)
        assert!(exist, InvariantError.PositionNotFound)
    
        let poolKey = position.poolKey
        let (_, pool) = wrappedGetPool(poolKey)
        let (_, upperTick) = wrappedGetTick(poolKey, position.upperTickIndex)
        let (_, lowerTick) = wrappedGetTick(poolKey, position.lowerTickIndex)

        let (_, _, mut mPosition, _, _) = modify(position, pool, lowerTick, upperTick, Liquidity{v: 0}, false, blockTimeStamp!())
        
        let tokensOwedX = mPosition.tokensOwedX
        let tokensOwedY = mPosition.tokensOwedY

        mPosition.tokensOwedX = TokenAmount{v: 0}
        mPosition.tokensOwedY = TokenAmount{v: 0}

        positions[key] = mPosition

        if (pool.reserveX == pool.reserveY) {
            Reserve(pool.reserveX).withdrawTwoAssets(caller, pool.poolKey.tokenX, pool.poolKey.tokenY, tokensOwedX, tokensOwedY)
        } else {
            Reserve(pool.reserveX).withdrawSingleAsset(caller, pool.poolKey.tokenX, tokensOwedX)
            Reserve(pool.reserveY).withdrawSingleAsset(caller, pool.poolKey.tokenY, tokensOwedY)    
        }
    }

    fn getSingleBatch(poolKey: PoolKey, index: U256) -> ByteVec {
        let mut batch = #
        let key = poolKeyBytes(poolKey) ++ toByteVec!(index)
        if (bitmap.contains!(key)) {
            let tickmapBatch = bitmap[key]
            for (let mut i = 0; i < CHUNKS_PER_BATCH; i = i + 1) {
                let offset = index * CHUNKS_PER_BATCH + i
                batch = batch ++ toByteVec!(offset) ++ b`break` ++ toByteVec!(tickmapBatch.chunks[i]) ++ b`break`
            }
        }
        return batch
    }


    pub fn getTickmapSlice(poolKey: PoolKey, lowerBatch: U256, upperBatch: U256, xToY: Bool) -> ByteVec {
        let mut slice = #
        let mut batchIndex = 0
        if (xToY) {
            for(batchIndex = lowerBatch; batchIndex < upperBatch; batchIndex = batchIndex + 1) {
                slice = slice ++ getSingleBatch(poolKey, batchIndex)
            }
        } else {
            for(batchIndex = upperBatch; batchIndex >= lowerBatch; batchIndex = batchIndex - 1) {
                slice = slice ++ getSingleBatch(poolKey, batchIndex)
            }
        }
        return slice
    }
    
    pub fn getAllPoolsForPair(token0: ByteVec, token1: ByteVec) -> ByteVec {
        let mut matchingPools = b``

        for(let mut i = 0; i < feeTierCount; i = i + 1) {
            let poolKey = newPoolKey(token0, token1, feeTiers.feeTiers[i])
            let (exist, pool) = getPool(poolKey)
            if(exist) {
                matchingPools = matchingPools ++ serializePool(pool)
            }
        }

        return matchingPools        
    }

    pub fn getPoolKeys(size: U256, offset: U256) -> (ByteVec, U256) {
        let mut slice = #
        let offsetEnd = offset + size
        let sliceEnd = if (offsetEnd > poolKeyCount) poolKeyCount else offsetEnd
        for(let mut i = offset + 1; i <= sliceEnd; i = i + 1) {
            let poolKey = poolKeys[i]
            slice = slice ++ poolKey.tokenX ++ b`break` ++ poolKey.tokenY ++ b`break` ++ toByteVec!(poolKey.feeTier.fee.v) ++ b`break` ++ toByteVec!(poolKey.feeTier.tickSpacing) ++ b`break`
        }
        return slice, poolKeyCount
    }

    pub fn getPositions(owner: Address, size: U256, offset: U256) -> ByteVec {
        let totalPositions = positionCount(owner)
        let mut slice = #
        for(let mut i = offset; i < totalPositions; i = i + 1) {
            if (i >= offset + size) {
                return slice ++ b`break` ++ toByteVec!(totalPositions)
            }
            let (_, position) = wrappedGetPosition(owner, i)
            let (_, pool) = wrappedGetPool(position.poolKey)
            slice = slice ++ serializePosition(position) ++ serializePool(pool)
        }
        return slice ++ b`break` ++ toByteVec!(totalPositions)
    }

    pub fn getUserPositionCount(owner: Address) -> U256 {
        return positionCount(owner)
    }

    pub fn getLiquidityTicks(poolKey: PoolKey, indexes: ByteVec, length: U256) -> ByteVec {
        let mut liquidityTicks = #

        let keyBytes = poolKeyBytes(poolKey)

        for (let mut i = 0; i < length; i = i + 1) {
            let index = toI256!(u256From4Byte!(byteVecSlice!(indexes, i * 4, (i + 1) * 4))) - GLOBAL_MAX_TICK
            
            let key = keyBytes ++ toByteVec!(index)
            let tick = ticks[key]
            liquidityTicks = liquidityTicks ++ toByteVec!(tick.index) ++ b`break` ++ toByteVec!(tick.liquidityChange.v) ++ b`break` ++ toByteVec!(tick.sign) ++ b`break`
        }

        return liquidityTicks
    }

    pub fn getLiquidityTicksAmount(poolKey: PoolKey, lowerTick: I256, upperTick: I256) -> U256 {
        let tickSpacing = poolKey.feeTier.tickSpacing
        clamm.checkTicks(lowerTick, upperTick, tickSpacing)

        let (minChunkIndex, minBit) = tickToPosition(lowerTick, tickSpacing)
        let (maxChunkIndex, maxBit) = tickToPosition(upperTick, tickSpacing)
        let minBatch = minChunkIndex / CHUNKS_PER_BATCH
        let maxBatch = maxChunkIndex / CHUNKS_PER_BATCH

        let mut amount = 0
        let mut i = 0
        if(minBatch == maxBatch) {
            let key = poolKeyBytes(poolKey) ++ toByteVec!(minBatch)

            if(!bitmap.contains!(key)) {
                return 0
            }

            let batch = bitmap[key].chunks
            let minIndexInBatch = minChunkIndex % CHUNKS_PER_BATCH
            let maxIndexInBatch = maxChunkIndex % CHUNKS_PER_BATCH

            if(minIndexInBatch == maxIndexInBatch) {
                amount = countActiveBitsInChunk(batch[minIndexInBatch], minBit, maxBit)
                return amount
            }

            amount = countActiveBitsInChunk(batch[minIndexInBatch], minBit, CHUNK_SIZE - 1)    
            for(i = minIndexInBatch + 1; i < maxIndexInBatch; i = i + 1) {
                amount = amount + countOnes(batch[i])
            }
            
            amount = amount + countActiveBitsInChunk(batch[maxIndexInBatch], 0, maxBit)
            return amount
        }

        amount = countActiveBitsInChunk(getChunk(minChunkIndex, poolKey), minBit, CHUNK_SIZE - 1)
        for(i = minChunkIndex + 1; i < maxChunkIndex; i = i + 1) {
            amount = amount + countOnes(getChunk(i, poolKey))
        }

        amount = amount + countActiveBitsInChunk(getChunk(maxChunkIndex, poolKey), 0, maxBit)

        return amount
    }

    pub fn getPositionWithAssociates(owner: Address, index: U256) -> (Position, Pool, Tick, Tick) {
        let (existPos, position) = wrappedGetPosition(owner, index)
        assert!(existPos, InvariantError.PositionNotFound)
        let (existP, pool) = wrappedGetPool(position.poolKey)
        assert!(existP, InvariantError.PoolNotFound)
        let (existLT, lowerTick) = wrappedGetTick(position.poolKey, position.lowerTickIndex)
        assert!(existLT, InvariantError.InvalidTickIndex)
        let (existUT, upperTick) = wrappedGetTick(position.poolKey, position.upperTickIndex)
        assert!(existUT, InvariantError.InvalidTickIndex)

        return position, pool, lowerTick, upperTick
    }
}
