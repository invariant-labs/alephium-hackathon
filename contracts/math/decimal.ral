struct SqrtPrice{
    mut v: U256
}

struct Liquidity{
    mut v: U256
}

struct FeeGrowth{
    mut v: U256
}

struct Percentage{
    mut v: U256
}

struct TokenAmount{
    mut v: U256
}

struct FixedPoint{
    mut v: U256
}

Abstract Contract Decimal() {
    const SqrtPriceScale = 24
    const LiquidityScale = 5
    const FeeGrowthScale = 28
    const FixedPointScale = 12
    const PercentageScale = 12
    const TokenAmountScale = 0
    const SqrtPriceDenominator = 1000000000000000000000000
    const LiquidiyDenominator = 100000
    const FeeGrowthDenominator = 10000000000000000000000000000
    const PercentageDenominator = 1000000000000
    const FixedPointDenominator = 1000000000000
    const GlobalMaxTick = 221818i
    const GlobalMinTick = -221818i
    const MaxSqrtPrice = 65535383934512647000000000000
    const MinSqrtPrice = 15258932000000000000


    enum DecimalError {
        TickOverBounds = 0
    }

    // TODO: In global constants update remove all these `get` functions, unecessary indirection to please the compiler

    pub fn getSqrtPriceScale() -> U256 {
        return SqrtPriceScale
    }

    pub fn getLiquidityScale() -> U256 {
        return LiquidityScale
    }

    pub fn getFeeGrowthScale() -> U256 {
        return FeeGrowthScale
    }

    pub fn getFixedPointScale() -> U256 {
        return FixedPointScale
    }

    pub fn getPercentageScale() -> U256 {
        return PercentageScale
    }

    pub fn getTokenAmountScale() -> U256 {
        return TokenAmountScale
    }

    pub fn getSqrtPriceDenominator() -> U256 {
        return SqrtPriceDenominator
    }

    pub fn getLiquidityDenominator() -> U256 {
        return LiquidiyDenominator
    }

    pub fn getFeeGrowthDenominator() -> U256 {
        return FeeGrowthDenominator
    }

    pub fn getPercentageDenominator() -> U256 {
        return PercentageDenominator
    }

    pub fn getGlobalMaxTick() -> I256 {
        return GlobalMaxTick
    }

    pub fn getGlobalMinTick() -> I256 {
        return GlobalMinTick
    }

    pub fn getMaxSqrtPrice(tickSpacing: U256) -> SqrtPrice {
        let maxTick = getMaxTick(tickSpacing)
        return calculateSqrtPrice(maxTick)
    }

    pub fn getMinSqrtPrice(tickSpacing: U256) -> SqrtPrice {
        let minTick = getMinTick(tickSpacing)
        return calculateSqrtPrice(minTick)
    }

    pub fn getMaxTick(tickSpacing: U256) -> I256 {
        let convertedTickSpacing = toI256!(tickSpacing)
        return GlobalMaxTick / convertedTickSpacing * convertedTickSpacing
    }

    pub fn getMinTick(tickSpacing: U256) -> I256 {
        let convertedTickSpacing = toI256!(tickSpacing)
        return GlobalMinTick / convertedTickSpacing * convertedTickSpacing
    }

    pub fn mul(l: U256, r:U256, rScale: U256) -> U256 {
        return (l * r) / rScale
    }

    pub fn mulUp(l: U256, r: U256, rScale: U256) -> U256 {
        return (l * r + rScale - 1) / rScale
    }
    
    pub fn divToTokenUp(l: U256, r: U256) -> U256 {
        return (((l * one(SqrtPriceScale) + (r - 1)) / r) + almostOne(SqrtPriceScale)) / one(SqrtPriceScale)
    }   

    pub fn divToToken(l: U256, r: U256) -> U256 {
        return ((l * one(SqrtPriceScale)) / r) / one(SqrtPriceScale)
    }   

    pub fn divUp(l: U256, r: U256) -> U256 {
        return l * one(SqrtPriceScale) + (r - 1) / r
    }   

    pub fn div(l: U256, r: U256) -> U256 {
        return l * one(SqrtPriceScale) / r
    }   

    pub fn almostOne(scale: U256) -> U256 {
        return 10 ** scale - 1
    }

    pub fn one(scale: U256) -> U256 {
        return 10 ** scale
    }

    pub fn sqrtPriceFromTick(
        tickIndex: I256
    ) -> SqrtPrice {
        let sqrtPrice = calculateSqrtPrice(tickIndex)
        return sqrtPrice
    }

    pub fn calculateSqrtPrice(
        tickIndex: I256
    ) -> SqrtPrice {
        let mut tickIndexAbs = 0
        if (tickIndex < 0i) {
            tickIndexAbs = toU256!((-1) * tickIndex)
        } else {
            tickIndexAbs = toU256!(tickIndex)
        }

        let mut sqrtPrice = FixedPoint{v: one(FixedPointScale)}

        assert!(tickIndexAbs <= toU256!(GlobalMaxTick), DecimalError.TickOverBounds)

        if (tickIndexAbs & 0x1 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1000049998750) / FixedPointDenominator
        }
        if (tickIndexAbs & 0x2 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1000100000000) / FixedPointDenominator
        }
        if (tickIndexAbs & 0x4 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1000200010000) / FixedPointDenominator
        }
        if (tickIndexAbs & 0x8 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1000400060004) / FixedPointDenominator
        }
        if (tickIndexAbs & 0x10 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1000800280056)  / FixedPointDenominator
        }
        if (tickIndexAbs & 0x20 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1001601200560) / FixedPointDenominator
        }
        if (tickIndexAbs & 0x40 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1003204964963) / FixedPointDenominator
        }
        if (tickIndexAbs & 0x80 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1006420201726) / FixedPointDenominator
        }
        if (tickIndexAbs & 0x100 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1012881622442) / FixedPointDenominator
        }
        if (tickIndexAbs & 0x200 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1025929181080) / FixedPointDenominator
        }
        if (tickIndexAbs & 0x400 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1052530684591) / FixedPointDenominator
        }
        if (tickIndexAbs & 0x800 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1107820842005) / FixedPointDenominator
        }
        if (tickIndexAbs & 0x1000 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1227267017980) / FixedPointDenominator
        }
        if (tickIndexAbs & 0x2000 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 1506184333421) / FixedPointDenominator
        }
        if (tickIndexAbs & 0x4000 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 2268591246242) / FixedPointDenominator
        }
        if (tickIndexAbs & 0x8000 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 5146506242525) / FixedPointDenominator
        }
        if (tickIndexAbs & 0x00010000 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 26486526504348) / FixedPointDenominator
        }
        if (tickIndexAbs & 0x00020000 != 0) {
            sqrtPrice.v = (sqrtPrice.v * 701536086265529) / FixedPointDenominator
        }

        if (tickIndex >= 0i) {
            return SqrtPrice{ v: rescale(sqrtPrice.v, FixedPointScale, SqrtPriceScale)}
        } else {
            let sqrtPriceInFixedPointScale = FixedPointDenominator * FixedPointDenominator / sqrtPrice.v
            return SqrtPrice{ v: rescale(sqrtPriceInFixedPointScale, FixedPointScale, SqrtPriceScale)}
        }
    }

    pub fn rescale(fromValue: U256, fromScale: U256, expectedScale: U256) -> U256 {
        if (expectedScale > fromScale) {
            let multiplierScale = expectedScale - fromScale
            return fromValue * (10 ** multiplierScale)
        } else {
            let denominatorScale = fromScale - expectedScale
            return fromValue / (10 ** denominatorScale)
        }
    }

    pub fn wrappingAdd(a: U256, b: U256) -> U256 {
        let u256Max = u256Max!()
        if (b > u256Max - a) {
            return (b - (u256Max - a)) - 1
        } else {
            return a + b
        }
    }

    pub fn wrappingSub(a: U256, b: U256) -> U256 {
        let u256Max = u256Max!()
        if (b > a) {
            return u256Max - (b - a) + 1
        } else {
            return a - b
        }
    }    

    pub fn calculateFeeGrowthInside(
        tickLowerIndex: I256,
        tickLowerFeeGrowthOutsideX: FeeGrowth,
        tickLowerFeeGrowthOutsideY: FeeGrowth,
        tickUpperIndex: I256,
        tickUpperFeeGrowthOutsideX: FeeGrowth,
        tickUpperFeeGrowthOutsideY: FeeGrowth,
        tickCurrent: I256,
        globalFeeGrowthX: FeeGrowth,
        globalFeeGrowthY: FeeGrowth
    ) -> (FeeGrowth,FeeGrowth) {
        let currentAboveLower = tickCurrent >= tickLowerIndex
        let currentBelowUpper = tickCurrent < tickUpperIndex

        let mut feeGrowthBelowX = FeeGrowth{v: 0}
        let mut feeGrowthBelowY = FeeGrowth{v: 0}
        
        let mut feeGrowthAboveX = FeeGrowth{v: 0}
        let mut feeGrowthAboveY = FeeGrowth{v: 0}

        if (currentAboveLower) {
            feeGrowthBelowX = tickLowerFeeGrowthOutsideX
            feeGrowthBelowY = tickLowerFeeGrowthOutsideY
        } else {
            feeGrowthBelowX.v = wrappingSub(globalFeeGrowthX.v, tickLowerFeeGrowthOutsideX.v)
            feeGrowthBelowY.v = wrappingSub(globalFeeGrowthY.v, tickLowerFeeGrowthOutsideY.v)
        }

        if (currentBelowUpper) {
            feeGrowthAboveX = tickUpperFeeGrowthOutsideX
            feeGrowthAboveY = tickUpperFeeGrowthOutsideY
        } else {
            feeGrowthAboveX.v = wrappingSub(globalFeeGrowthX.v, tickUpperFeeGrowthOutsideX.v)
            feeGrowthAboveY.v = wrappingSub(globalFeeGrowthY.v, tickUpperFeeGrowthOutsideY.v)
        }

        let feeGrowthInsideX = FeeGrowth{v: wrappingSub(wrappingSub(globalFeeGrowthX.v, feeGrowthBelowX.v), feeGrowthAboveX.v)}
        let feeGrowthInsideY = FeeGrowth{v: wrappingSub(wrappingSub(globalFeeGrowthY.v, feeGrowthBelowY.v), feeGrowthAboveY.v)}

        return feeGrowthInsideX, feeGrowthInsideY
    }
}
