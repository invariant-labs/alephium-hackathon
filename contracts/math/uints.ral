struct U512 {
    mut higher: U256,
    mut lower: U256
}

Abstract Contract Uints () {
    const MaxU256 = 115792089237316195423570985008687907853269984665640564039457584007913129639935
    const WordSize = 256

    enum ArithmeticError {
        CastOverflow = 100001
        AddOverflow = 100002
        SubUnderflow = 100003
        DivNotPositiveDivisor = 100005
        DivNotPositiveDenominator = 100006
        MulNotPositiveDenominator = 100007
    }
    
    pub fn toU512(value: U256) -> U512 {
        return U512 {
            higher: 0,
            lower: value
        }
    }

    pub fn toU256(value: U512) -> U256 {
        assert!(value.higher == 0, ArithmeticError.CastOverflow)
        return value.lower
    }

    pub fn bigAdd256(a: U256, b: U256) -> U512 {
        let (lower, overflow) = overflowingAdd(a, b)
        
        if(overflow == 1) {
            return U512 { higher: 1, lower: lower }
        }

        return U512 { higher: 0, lower: lower }
    }

    pub fn newBigAdd256(a: U256, b: U256) -> U512 {
        let lower = a |+| b

        if (lower < b) {
            return U512 { higher: 1, lower: lower }
        }

        return U512 { higher: 0, lower: lower }
    }

    pub fn bigAdd(a: U512, b: U256) -> U512 {
        let (lower, lowerOverflow) = overflowingAdd(a.lower, b)
        let (higher, higherOverflow) = overflowingAdd(a.higher, lowerOverflow)

        assert!(higherOverflow == 0, ArithmeticError.AddOverflow)

        return U512 { higher: higher, lower: lower }
    }

    pub fn newBigAdd(a: U512, b: U256) -> U512 {
        let lower = a.lower |+| b
        let mut higher = a.higher

        if (lower < b) {
            higher = higher + 1
        }

        return U512 { higher: higher, lower: lower }
    }

    pub fn bigAdd512(a: U512, b: U512) -> U512 {
        let (lower, lowerOverflow) = overflowingAdd(a.lower, b.lower)
        let (higherIntermediate, higherIntermediateOverflow) = overflowingAdd(a.higher, b.higher)
        let (higher, higherOverflow) = overflowingAdd(higherIntermediate, lowerOverflow)

        assert!(higherIntermediateOverflow != 1 && higherOverflow != 1, ArithmeticError.AddOverflow)

        return U512 { higher: higher, lower: lower }
    }

    pub fn newBigAdd512(a: U512, b: U512) -> U512 {
        let lower = a.lower |+| b.lower
        let mut higher = a.higher + b.higher

        if (lower < b.lower) {
            higher = higher + 1
        }

        return U512 { higher: higher, lower: lower }
    }

    pub fn bigSub512(a: U512, b: U512) -> U512 {
        assert!(b.higher <= a.higher, ArithmeticError.SubUnderflow)

        let mut higher = a.higher - b.higher
        let mut lower = 0

        if (b.lower > a.lower) {
            assert!(higher > 0, ArithmeticError.SubUnderflow)

            higher = higher - 1
            lower = MaxU256 - b.lower + a.lower + 1
        } else {
            lower = a.lower - b.lower
        }

        return U512 { higher: higher, lower: lower }
    }

    pub fn newBigSub512(a: U512, b: U512) -> U512 {
        let lower = a.lower |-| b.lower
        let mut higher = a.higher - b.higher

        if (a.lower < b.lower) {
            higher = higher - 1
        }

        return U512 { higher: higher, lower: lower }
    }

    pub fn bigDivWrapper(a: U512, b: U256, bDenominator: U256) -> U512 {
        assert!(b != 0, ArithmeticError.DivNotPositiveDivisor)
        assert!(bDenominator != 0, ArithmeticError.DivNotPositiveDenominator)

        let mut q = U512 { higher: 0, lower: 0 }
        let mut r = 0

        let extA = bigMul(a, bDenominator)

        if (extA.higher == 0) {
            return U512 { higher: 0, lower: extA.lower / b }
        }

        // Optimalization case
        if (b == MaxU256) {
            return U512 { higher: 0, lower: extA.higher }
        }

        if (b == 1) {
            return extA
        }

        let mut uHigh = extA.higher
        let mut uLow = extA.lower
        let v = b

        let mut j = 512
        while (j > 0) {
            j = j - 1
            r = r << 1

            if (((uHigh >> 255) & 1) != 0) {
                r = r | 1
            }
            uHigh = uHigh << 1

            if ((uLow >> 255) != 0) {
                uHigh = uHigh | 1
            }
            uLow = uLow << 1
            if (r >= v) {
                r = r - v
                if (j >= 256) {
                    q.higher = q.higher | (1 << (j - 256))
                } else {
                    q.lower = q.lower | (1 << j)
                }
            }
        }

        return q
    }

    pub fn newBigDivWrapper(a: U512, b: U256) -> U512 {
        assert!(b != 0, ArithmeticError.DivNotPositiveDivisor)

        if (b == 1) {
            return a
        }

        let mut mutA = a
        let mut result = U512 { higher: 0, lower: 0 }

        let q = (0 |-| b) / b + 1
        let r = (0 |-| b) % b

        while(mutA.higher != 0) {
            let mut mul = newBigMul256(mutA.higher, q)
            result = newBigAdd512(result, mul)
            mul = newBigMul256(mutA.higher, r)
            mutA = newBigAdd512(mul, U512 { higher: 0, lower: mutA.lower })
        }

        result = newBigAdd512(result, U512 { higher: 0, lower: mutA.lower / b })

        return result
    }

    pub fn bigDiv(a: U512, b: U256, bDenominator: U256) -> U512 {
        return bigDivWrapper(a, b, bDenominator)
    }

    pub fn newBigDiv(a: U512, b: U256, bDenominator: U256) -> U512 {
        assert!(bDenominator != 0, ArithmeticError.DivNotPositiveDenominator)
        let result = newBigMul(a, bDenominator)
        return newBigDivWrapper(result, b)
    }

    pub fn bigDivUp(a: U512, b: U256, bDenominator: U256) -> U512 {
        assert!(b != 0, ArithmeticError.DivNotPositiveDivisor)
        assert!(bDenominator != 0, ArithmeticError.DivNotPositiveDenominator)

        let mut result = bigMul(a, bDenominator)
        result = bigAdd(result, b - 1)
        return bigDivWrapper(result, b, 1)
    }

    pub fn newBigDivUp(a: U512, b: U256, bDenominator: U256) -> U512 {
        assert!(bDenominator != 0, ArithmeticError.DivNotPositiveDenominator)
        let mut result = newBigMul(a, bDenominator)
        result = newBigAdd(result, b - 1)
        return newBigDivWrapper(result, b)
    }

    pub fn bigDiv512(dividend: U512, divisor: U512, divisorDenominator: U256) -> U512 {
        let mut q = U512 { higher: 0, lower: 0 }
        let mut r = U512 { higher: 0, lower: 0 }

        assert!(!isZero(divisor), ArithmeticError.DivNotPositiveDivisor)
        assert!(divisorDenominator != 0, ArithmeticError.DivNotPositiveDenominator)

        let extDividend = bigMul(dividend, divisorDenominator)
        let mut uHigh = extDividend.higher
        let mut uLow = extDividend.lower

        let v = divisor
    
        let mut j = 512
        while (j > 0) {
            j = j - 1
            r = bigShl(r, 1)
    
            if (((uHigh >> 255) & 1) != 0) {
                r = bitOrLower(r, 1)
            }

            uHigh = uHigh << 1
    
            if ((uLow >> 255) != 0) {
                uHigh = uHigh | 1
            }
            uLow = uLow << 1
    
            if (isGreaterEqual(r,v)) {
                r = bigSub512(r, v)
                if (j >= 256) {
                    q.higher = q.higher | (1 << (j - 256))
                } else {
                    q.lower = q.lower | (1 << j)
                }
            }
        }

        return q
    }

    pub fn bigDivUp512(dividend: U512, divisor: U512, divisorDenominator: U256) -> U512 {
        let mut result = bigMul(dividend, divisorDenominator)
        result = bigAdd512(result, divisor)
        result.lower = result.lower - 1
        return bigDiv512(result, divisor, 1)
    }

    pub fn bigMul256(a: U256, b: U256) -> U512 {
        let aLower = low128(a)
        let aHigher = high128(a)
        let bLower = low128(b)
        let bHigher = high128(b)

        let aLowerBLower = aLower * bLower
        let aHigherBHigher = aHigher * bHigher
        let aHigherBLower = aHigher * bLower
        let aLowerBHigher = aLower * bHigher

        let lowerLower = low128(aLowerBLower)
        let lowerHigher = high128(aLowerBLower) + low128(aLowerBHigher) + low128(aHigherBLower)
        let higherLower = low128(aHigherBHigher) + high128(aLowerBHigher) + high128(aHigherBLower)
        let higherHigher = high128(aHigherBHigher)
        let lower = lowerLower + (lowerHigher << 128)
        let higher = high128(lowerHigher) + higherLower + (higherHigher << 128)

        return U512 { higher: higher, lower: lower }
    }

    pub fn newBigMul256(a: U256, b: U256) -> U512 {
        let mulMod = mulModN!(a, b, u256Max!())
        let lower = a |*| b
        let mut higher = mulMod |-| lower

        if (mulMod < lower) {
            higher = higher - 1
        }

        return U512 { higher: higher, lower: lower }
    }

    pub fn bigMul(a: U512, b: U256) -> U512 {        
        let result = bigMul256(a.lower, b)
        let higher = toU256(bigMul256(a.higher, b))

        return bigAdd512(result, U512 { higher: higher, lower: 0 })
    }

    pub fn newBigMul(a: U512, b: U256) -> U512 {        
        let result = newBigMul256(a.lower, b)
        let higher = toU256(newBigMul256(a.higher, b))

        return newBigAdd512(result, U512 { higher: higher, lower: 0 })
    }

    pub fn bigMulDiv256(a: U256, b: U256, bDenominator: U256) -> U512 {
        assert!(bDenominator != 0, ArithmeticError.MulNotPositiveDenominator)

        let mut result = bigMul256(a, b)
        result = bigDiv(result, bDenominator, 1)

        return result
    }

    pub fn newBigMulDiv256(a: U256, b: U256, bDenominator: U256) -> U512 {
        assert!(bDenominator != 0, ArithmeticError.MulNotPositiveDenominator)

        let mut result = newBigMul256(a, b)
        result = newBigDiv(result, bDenominator, 1)

        return result
    }

    pub fn bigMulDivUp256(a: U256, b: U256, bDenominator: U256) -> U512 {
        assert!(bDenominator != 0, ArithmeticError.MulNotPositiveDenominator)
        let mut result = bigMul256(a, b)

        result = bigAdd512(result, toU512(bDenominator - 1))
        result = bigDiv(result, bDenominator, 1)

        return result
    }

    pub fn newBigMulDivUp256(a: U256, b: U256, bDenominator: U256) -> U512 {
        assert!(bDenominator != 0, ArithmeticError.MulNotPositiveDenominator)
        let mut result = newBigMul256(a, b)

        result = newBigAdd512(result, toU512(bDenominator - 1))
        result = newBigDiv(result, bDenominator, 1)

        return result
    }
    
    pub fn overflowingAdd(a: U256, b: U256) -> (U256, U256) {
        let lower = a |+| b

        if (lower < b) {
            return lower, 1
        }

        return lower, 0
    }

    pub fn low128(a: U256) -> U256 {
        return a & ((1 << 128) - 1)
    }
    
    pub fn high128(a: U256) -> U256 {
        return a >> 128
    }

    pub fn bigDivToTokenUp(nominator: U512, denominator: U256, sqrtPriceDenominator: U256) -> U256 {
        let mut result = bigMul(nominator, sqrtPriceDenominator)
        result = bigAdd(result, denominator - 1)
        result = bigDiv(result, denominator, 1)
        result = bigAdd(result, sqrtPriceDenominator - 1)
        result = bigDiv(result, sqrtPriceDenominator, 1)

        return toU256(result)
    }

    pub fn bigDivToToken(nominator: U512, denominator: U256, sqrtPriceDenominator: U256) -> U256 {
        let mut result = bigMul(nominator, sqrtPriceDenominator)
        result = bigDiv(result, denominator, 1)
        result = bigDiv(result, sqrtPriceDenominator, 1)

        return toU256(result)
    }

    pub fn isGreaterEqual(v: U512, compareTo: U512) -> Bool {
        if (v.higher > compareTo.higher || (v.higher == compareTo.higher && v.lower >= compareTo.lower)) {
            return true
        }

        return false
    }

    pub fn bigShl(mut v: U512, n: U256) -> U512 {
        if (n >= WordSize) {
            v.higher = v.lower << (n - WordSize)
            v.lower = 0
        } else {
            v.higher = (v.higher << n) | (v.lower >> (WordSize - n))
            v.lower = v.lower << n
        }
        
        return v
    }

    pub fn bitOrLower(v: U512, n: U256) -> U512 {
        return U512 { higher: v.higher, lower: v.lower | n }
    }
    
    pub fn bigRescale(fromValue: U256, fromScale: U256, expectedScale: U256) -> U512 {
        if (expectedScale > fromScale) {
            let multiplierScale = expectedScale - fromScale
            return bigMul256(fromValue, 10 ** multiplierScale)
        } else {
            let denominatorScale = fromScale - expectedScale
            // Most likely does not require an extenstion to U512
            return bigDiv(toU512(fromValue), 10 ** denominatorScale, 1)
        }
    }

    pub fn isZero(a: U512) -> Bool {
        return a.higher == 0 && a.lower == 0
    }
}                           

