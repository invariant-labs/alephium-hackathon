Contract Utils() extends Uints(), Decimal(), Log(), PoolKeyHelper(), FeeTierHelper(), Tickmap() {
    // Placeholder to allow exporting tickmap methods
    // Key: poolKey ++ subcontract index
    mapping[ByteVec, TickmapBatch] bitmap

    pub fn getLiquidityByX(x: TokenAmount, lowerTick: I256, upperTick: I256, currentSqrtPrice: SqrtPrice, roundingUp: Bool) -> SingleTokenLiquidity {
        assert!(lowerTick >= GLOBAL_MIN_TICK && upperTick <= GLOBAL_MAX_TICK, UtilsError.InvalidTickIndex)
        let lowerSqrtPrice = calculateSqrtPrice(lowerTick)
        let upperSqrtPrice = calculateSqrtPrice(upperTick)

        return getLiquidityByXSqrtPrice(x, lowerSqrtPrice, upperSqrtPrice, currentSqrtPrice, roundingUp)
    }

    pub fn getLiquidityByXSqrtPrice(x: TokenAmount, lowerSqrtPrice: SqrtPrice, upperSqrtPrice: SqrtPrice, currentSqrtPrice: SqrtPrice, roundingUp: Bool) -> SingleTokenLiquidity {
        assert!(upperSqrtPrice.v >= currentSqrtPrice.v, UtilsError.UpperLTCurrentSqrtPrice)

        let mut nominator = U512 { higher: 0, lower: 0}
        let mut denominator = 0
        let mut liquidity = Liquidity{v: 0}
        if (currentSqrtPrice.v < lowerSqrtPrice.v) {
            nominator = bigMulDiv256(lowerSqrtPrice.v, upperSqrtPrice.v, SQRT_PRICE_DENOMINATOR)
            denominator = upperSqrtPrice.v - lowerSqrtPrice.v
            liquidity.v = toU256(bigDiv(bigMul(bigMul(nominator, x.v), LIQUIDITY_DENOMINATOR), denominator, 1))
            return SingleTokenLiquidity { l: liquidity, amount: TokenAmount{v: 0} }
        }

        nominator = bigMulDiv256(currentSqrtPrice.v, upperSqrtPrice.v, SQRT_PRICE_DENOMINATOR)
        denominator = upperSqrtPrice.v - currentSqrtPrice.v
        liquidity.v = toU256(bigDiv(bigMul(bigMul(nominator, x.v), LIQUIDITY_DENOMINATOR), denominator, 1))
        let sqrtPriceDiff = SqrtPrice{v: currentSqrtPrice.v - lowerSqrtPrice.v}
        let y = calculateY(sqrtPriceDiff, liquidity, roundingUp)
        return SingleTokenLiquidity { l: liquidity, amount: y }
    }

    pub fn calculateY(sqrtPriceDiff: SqrtPrice, liquidity: Liquidity, roundingUp: Bool) -> TokenAmount {
        let shiftedLiquidity = liquidity.v / LIQUIDITY_DENOMINATOR
        if (roundingUp) {
            return TokenAmount{v: (sqrtPriceDiff.v * shiftedLiquidity + almostOne(SQRT_PRICE_SCALE)) / SQRT_PRICE_DENOMINATOR}
        } else {
            return TokenAmount{v: sqrtPriceDiff.v * shiftedLiquidity / SQRT_PRICE_DENOMINATOR}
        }
    }

    pub fn getLiquidityByY(y: TokenAmount, lowerTick: I256, upperTick: I256, currentSqrtPrice: SqrtPrice, roundingUp: Bool) -> SingleTokenLiquidity {
        assert!(lowerTick >= GLOBAL_MIN_TICK && upperTick <= GLOBAL_MAX_TICK, UtilsError.InvalidTickIndex)
        let lowerSqrtPrice = calculateSqrtPrice(lowerTick)
        let upperSqrtPrice = calculateSqrtPrice(upperTick)

        return getLiquidityByYSqrtPrice(y, lowerSqrtPrice, upperSqrtPrice, currentSqrtPrice, roundingUp)
    }

    pub fn getLiquidityByYSqrtPrice(y: TokenAmount, lowerSqrtPrice: SqrtPrice, upperSqrtPrice: SqrtPrice, currentSqrtPrice: SqrtPrice, roundingUp: Bool) -> SingleTokenLiquidity {
        assert!(currentSqrtPrice.v >= lowerSqrtPrice.v, UtilsError.CurrentLTLowerSqrtPrice)

        let mut sqrtPriceDiff = SqrtPrice{v: 0}
        let mut liquidity = Liquidity{v: 0}

        if (upperSqrtPrice.v <= currentSqrtPrice.v) {
            sqrtPriceDiff.v = upperSqrtPrice.v - lowerSqrtPrice.v
            liquidity.v = toU256(bigDiv(bigMul(bigMul256(y.v, SQRT_PRICE_DENOMINATOR), LIQUIDITY_DENOMINATOR), sqrtPriceDiff.v, 1))
            return SingleTokenLiquidity { l: liquidity, amount: TokenAmount{v: 0} }
        }

        sqrtPriceDiff.v = currentSqrtPrice.v - lowerSqrtPrice.v
        liquidity.v = toU256(bigDiv(bigMul(bigMul256(y.v, SQRT_PRICE_DENOMINATOR), LIQUIDITY_DENOMINATOR), sqrtPriceDiff.v, 1))
        let denominator = SqrtPrice{v: (currentSqrtPrice.v * upperSqrtPrice.v )/ SQRT_PRICE_DENOMINATOR}
        let nominator = SqrtPrice{v: upperSqrtPrice.v - currentSqrtPrice.v}
        let x = calculateX(nominator, denominator, liquidity, roundingUp)
        return SingleTokenLiquidity { l: liquidity, amount: x }
    }

    pub fn calculateX(nominator: SqrtPrice, denominator: SqrtPrice, liquidity: Liquidity, roundingUp: Bool) -> TokenAmount {
        let common = bigMulDiv256(liquidity.v, nominator.v, denominator.v)
        if (roundingUp) {
            return TokenAmount{v: toU256(bigDiv(bigAdd(common, almostOne(LIQUIDITY_SCALE)), LIQUIDITY_DENOMINATOR, 1))}
        } else {
            return TokenAmount{v: toU256(bigDiv(common, LIQUIDITY_DENOMINATOR, 1))}
        }
    }

    pub fn getLiquidity(x: TokenAmount, y: TokenAmount, lowerTick: I256, upperTick: I256, currentSqrtPrice: SqrtPrice, roundingUp: Bool) -> LiquidityResult {
        assert!(lowerTick >= GLOBAL_MIN_TICK && upperTick <= GLOBAL_MAX_TICK, UtilsError.InvalidTickIndex)
        let lowerSqrtPrice = calculateSqrtPrice(lowerTick)
        let upperSqrtPrice = calculateSqrtPrice(upperTick)

        let mut resultByX = SingleTokenLiquidity { l: Liquidity{v: 0}, amount: TokenAmount{v: 0}}
        let mut resultByY = SingleTokenLiquidity { l: Liquidity{v: 0}, amount: TokenAmount{v: 0}}

        if (upperSqrtPrice.v < currentSqrtPrice.v) {
            resultByY = getLiquidityByYSqrtPrice(y, lowerSqrtPrice, upperSqrtPrice, currentSqrtPrice, roundingUp)
            return LiquidityResult { x:  resultByY.amount, y: y, l: resultByY.l } 
        } else if (currentSqrtPrice.v < lowerSqrtPrice.v) {
            resultByX = getLiquidityByXSqrtPrice(x, lowerSqrtPrice, upperSqrtPrice, currentSqrtPrice, roundingUp)
            return LiquidityResult { x: x, y: resultByX.amount, l: resultByX.l }
        } else {
            // do nothing
        }

        resultByY = getLiquidityByYSqrtPrice(y, lowerSqrtPrice, upperSqrtPrice, currentSqrtPrice, roundingUp)
        resultByX = getLiquidityByXSqrtPrice(x, lowerSqrtPrice, upperSqrtPrice, currentSqrtPrice, roundingUp)

        if(resultByY.l.v < resultByX.l.v) {
            return LiquidityResult { x: resultByY.amount, y: resultByX.amount, l: resultByY.l }
        } else {
            return LiquidityResult { x: resultByY.amount, y: resultByX.amount, l: resultByX.l }
        }
    }

    pub fn isTokenX(candidate: Address, compareTo: Address) -> Bool {
        let candidateBytes = toByteVec!(candidate)
        let compareToBytes = toByteVec!(compareTo)

        assert!(candidateBytes != compareToBytes, InvariantError.TokensAreSame)

        for (let mut i = 0; i <= size!(candidateBytes); i = i + 1) {
            if (u256From1Byte!(byteVecSlice!(candidateBytes, i, i + 1)) < u256From1Byte!(byteVecSlice!(compareToBytes, i, i + 1))) {
                return true
            } else {
                return false
            }
        }

        return true
    }

    pub fn calculateFee(
        tickLowerIndex: I256,
        tickLowerFeeGrowthOutsideX: FeeGrowth,
        tickLowerFeeGrowthOutsideY: FeeGrowth,
        tickUpperIndex: I256,
        tickUpperFeeGrowthOutsideX: FeeGrowth,
        tickUpperFeeGrowthOutsideY: FeeGrowth,
        tickCurrent: I256,
        globalFeeGrowthX: FeeGrowth,
        globalFeeGrowthY: FeeGrowth,
        positionFeeGrowthInsideX: FeeGrowth,
        positionFeeGrowthInsideY: FeeGrowth,
        positionLiquidity: Liquidity
    ) -> (TokenAmount, TokenAmount) {
        let (feeGrowthInsideX, feeGrowthInsideY) = calculateFeeGrowthInside(
            tickLowerIndex,
            tickLowerFeeGrowthOutsideX,
            tickLowerFeeGrowthOutsideY,
            tickUpperIndex,
            tickUpperFeeGrowthOutsideX,
            tickUpperFeeGrowthOutsideY,
            tickCurrent,
            globalFeeGrowthX,
            globalFeeGrowthY
        )

        let tokensOwedX = toFee(FeeGrowth{v: wrappingSub(feeGrowthInsideX.v, positionFeeGrowthInsideX.v)}, positionLiquidity)
        let tokensOwedY = toFee(FeeGrowth{v: wrappingSub(feeGrowthInsideY.v, positionFeeGrowthInsideY.v)}, positionLiquidity)

        return tokensOwedX, tokensOwedY
    }

    pub fn toFee(feeGrowth: FeeGrowth, liquidity: Liquidity) -> TokenAmount {
        return TokenAmount{v: toU256(bigMulDiv256(feeGrowth.v, liquidity.v, one(FEE_GROWTH_SCALE + LIQUIDITY_SCALE)))}
    }

    pub fn bitPositionToTick(
        chunk: U256,
        bit: U256,
        tickSpacing: U256
    ) -> I256 {
        let tickRangeLimit = GLOBAL_MAX_TICK - (GLOBAL_MAX_TICK % toI256!(tickSpacing))
        return toI256!(chunk * CHUNK_SIZE * tickSpacing + (bit * tickSpacing)) - tickRangeLimit
    }
}