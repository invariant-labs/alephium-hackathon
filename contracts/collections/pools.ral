Abstract Contract Pools(clamm: CLAMM) extends PoolKeyHelper(), PoolHelper(clamm) {
    @using(preapprovedAssets = true)
    fn addPool(
        originalCaller: Address,
        poolKey: PoolKey,
        initSqrtPrice: SqrtPrice,
        initTick: I256,
        currentTimestamp: U256,
        feeReceiver: Address
    ) -> () {
        assert!(!containsPool(poolKey), InvariantError.PoolAlreadyExist)

        let key = poolKeyBytes(poolKey)
        let (reserveX, reserveY) = handleReserves{originalCaller -> ALPH: mapEntryDeposit!() * 3}(originalCaller, poolKey.tokenX, poolKey.tokenY)
        let state = Pool {
            poolKey: poolKey,
            liquidity: Liquidity{v: 0},
            sqrtPrice: initSqrtPrice,
            currentTickIndex: initTick,
            feeGrowthGlobalX: FeeGrowth{v: 0},
            feeGrowthGlobalY: FeeGrowth{v: 0},
            feeProtocolTokenX: TokenAmount{v: 0},
            feeProtocolTokenY: TokenAmount{v: 0},
            startTimestamp: currentTimestamp,
            lastTimestamp: currentTimestamp,
            feeReceiver: feeReceiver,
            reserveX: reserveX,
            reserveY: reserveY
        }
        
        pools.insert!(originalCaller, key, state)
    }

    fn containsPool(poolKey: PoolKey) -> Bool {
        let key = poolKeyBytes(poolKey)
        return pools.contains!(key)
    }

    fn wrappedGetPool(poolKey: PoolKey) -> (Bool, Pool) {
        let key = poolKeyBytes(poolKey)
        if(containsPool(poolKey)) {
            return true, pools[key]
        } else {
            let nullId = addressToContractId!(nullContractAddress!())
            return false,
            Pool {
                poolKey: PoolKey {
                    tokenX: nullId,
                    tokenY: nullId,
                    feeTier: FeeTier {
                        fee: Percentage{v: 0},
                        tickSpacing: 0
                    }
                },
                liquidity: Liquidity{v: 0},
                sqrtPrice: SqrtPrice{v: 0},
                currentTickIndex: 0i,
                feeGrowthGlobalX: FeeGrowth{v: 0},
                feeGrowthGlobalY: FeeGrowth{v: 0},
                feeProtocolTokenX: TokenAmount{v: 0},
                feeProtocolTokenY: TokenAmount{v: 0},
                startTimestamp: 0,
                lastTimestamp: 0,
                feeReceiver: nullContractAddress!(),
                reserveX: nullId,
                reserveY: nullId
            }
        } 
    }
}
