Abstract Contract Pools(clamm: CLAMM) extends PoolKeyHelper(), PoolHelper(clamm) {
    @using(preapprovedAssets = true)
    fn addPool(
        originalCaller: Address,
        poolKey: ByteVec,
        tokenX: ByteVec,
        tokenY: ByteVec,
        fee: U256,
        tickSpacing: U256,
        initSqrtPrice: U256,
        initTick: I256,
        currentTimestamp: U256,
        feeReceiver: Address
    ) -> () {
        assert!(!containsPool(poolKey), InvariantError.PoolAlreadyExist)

        let state = Pool {
            tokenX: tokenX,
            tokenY: tokenY,
            fee: fee,
            tickSpacing: tickSpacing,
            liquidity: 0,
            sqrtPrice: initSqrtPrice,
            currentTickIndex: initTick,
            feeGrowthGlobalX: 0,
            feeGrowthGlobalY: 0,
            feeProtocolTokenX: 0,
            feeProtocolTokenY: 0,
            startTimestamp: currentTimestamp,
            lastTimestamp: currentTimestamp,
            feeReceiver: feeReceiver
        }
        pools.insert!(originalCaller, poolKey, state)
    }

    fn containsPool(poolKey: ByteVec) -> Bool {
        return pools.contains!(poolKey)
    }

    fn setSqrtPrice(poolKey: ByteVec, sqrtPrice: U256) -> () {
        let mut state = pools[poolKey]
        state.sqrtPrice = sqrtPrice
        pools[poolKey] = state
    }

    fn setCurrentTickIndex(poolKey: ByteVec, tickIndex: I256) -> () {
        let mut state = pools[poolKey]
        state.currentTickIndex = tickIndex
        pools[poolKey] = state
    }

    fn setLastTimestamp(poolKey: ByteVec, timestamp: U256) -> () {
        let mut state = pools[poolKey]
        state.lastTimestamp = timestamp
        pools[poolKey] = state
    }
    
    fn setFeeReceiver(poolKey: ByteVec, newReceiver: Address) -> () {
        let mut state = pools[poolKey]
        state.feeReceiver = newReceiver
        pools[poolKey] = state
    }

    fn wrappedGetPool(poolKey: ByteVec) -> (Bool, Pool) {
        if(containsPool(poolKey)) {
            return true, pools[poolKey]
        } else {   
            return false,
            Pool {
                tokenX: #,
                tokenY: #,
                fee: 0,
                tickSpacing: 0,
                liquidity: 0,
                sqrtPrice: 0,
                currentTickIndex: 0i,
                feeGrowthGlobalX: 0,
                feeGrowthGlobalY: 0,
                feeProtocolTokenX: 0,
                feeProtocolTokenY: 0,
                startTimestamp: 0,
                lastTimestamp: 0,
                feeReceiver: byteVecToAddress!(#)
            }
        } 
    }

    fn wrappedAddFee(poolKey: ByteVec, feeAmount: U256, xToY: Bool, fee: U256) -> () {
        let pool = pools[poolKey]
        let updatedPool = addFee(pool, feeAmount, xToY, fee)
        pools[poolKey] = updatedPool
    }

    fn wrappedCrossTick(
        poolKey: ByteVec,
        nextSqrtPrice: U256,
        swapLimit: U256,
        tickIndex: I256,
        remainingAmount: U256,
        byAmountIn: Bool,
        xToY: Bool,
        protocolFee: U256,
        fee: U256,
        tickSpacing: U256
    ) -> (U256, U256, Bool) {
        let (totalAmount, amountLeft, hasCrossed, updatedPool) = crossTick(
            pools[poolKey],
            nextSqrtPrice,
            swapLimit,
            tickIndex,
            remainingAmount,
            byAmountIn,
            xToY,
            protocolFee,
            fee,
            tickSpacing
        )
        pools[poolKey] = updatedPool

        return totalAmount, amountLeft, hasCrossed
    }

    fn wrappedCrossLiquidityUpdate(
        poolKey: ByteVec,
        add: Bool,
        liquidityDelta: U256
    ) -> () {
        let pool = crossLiquidityUpdate(pools[poolKey], add, liquidityDelta)
        pools[poolKey] = pool
        return
    }

    fn wrappedUpdateLiquidity(poolKey: ByteVec, liquidityDelta: U256, liquiditySign: Bool, upperTick: I256, lowerTick: I256) -> (U256, U256) {
        let (x, y, pool) = updateLiquidity(pools[poolKey], liquidityDelta, liquiditySign, upperTick, lowerTick)
        pools[poolKey] = pool
        return x, y
    }

    fn wrappedGetProtocolFee(poolKey: ByteVec) -> (U256, U256) {
        let (feeProtocolTokenX, feeProtocolTokenY, pool) = getAndUpdateProtocolFee(pools[poolKey])
        pools[poolKey] = pool
        return feeProtocolTokenX, feeProtocolTokenY
    }
}