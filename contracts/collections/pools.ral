Contract Pools(
    poolTemplateContractId: ByteVec,
    clammContractId: ByteVec,
    mut invariantId: Address,
    mut positionsId: Address,
    mut swapUtilsId: Address,
    mut areAdminsSet: Bool
) extends PoolKeyHelper() {
    enum PoolsError {
        PoolAlreadyExist = 301
        NotAdmin = 302
        AdminsAreSet = 303
    }

    @using(updateFields = true, checkExternalCaller = false)
    pub fn setAdmins(invariant: Address, positions: Address, swapUtils: Address) -> () {
        assert!(!areAdminsSet, PoolsError.AdminsAreSet)
        invariantId = invariant
        positionsId = positions
        swapUtilsId = swapUtils
        areAdminsSet = true
    }


    @using(preapprovedAssets = true)
    pub fn add(originalCaller: Address, poolKey: ByteVec, tokenX: ByteVec, tokenY: ByteVec, tickSpacing: U256, initSqrtPrice: U256, initTick: I256, currentTimestamp: U256, feeReceiver: Address) -> () {
        let callerId = callerAddress!()
        checkCaller!((callerId == invariantId || callerId == positionsId || callerId == swapUtilsId) && areAdminsSet, PoolsError.NotAdmin)

        assert!(!contractExists!(subContractId!(poolKey)), PoolsError.PoolAlreadyExist)

        let (encodedImmFields, encodedMutFields) = Pool.encodeFields!(selfAddress!(), tokenX, tokenY, tickSpacing, 0, initSqrtPrice, initTick, 0, 0, 0, 0, currentTimestamp, currentTimestamp, feeReceiver, clammContractId)
        copyCreateSubContract!{originalCaller -> ALPH: 1 alph}(
            poolKey,
            poolTemplateContractId,
            encodedImmFields,
            encodedMutFields
        )
    }

    pub fn getCurrentSqrtPrice(poolKey: ByteVec) -> U256 {
        return Pool(subContractId!(poolKey)).getPoolCurrentSqrtPrice()
    }

    pub fn getCurrentTickIndex(poolKey: ByteVec) -> I256 {
        return Pool(subContractId!(poolKey)).getPoolCurrentTickIndex()
    }

    pub fn getCurrentLiquidity(poolKey: ByteVec) -> U256 {
        return Pool(subContractId!(poolKey)).getPoolLiquidity()
    }

    pub fn getFeeGrowthGlobal(poolKey: ByteVec) -> (U256, U256) {
        let contract = Pool(subContractId!(poolKey))
        return contract.getFeeGrowthGlobalX(), contract.getFeeGrowthGlobalY()
    }

    pub fn getStartTimestamp(poolKey: ByteVec) -> U256 {
        return Pool(subContractId!(poolKey)).getStartTimestamp()
    }

    pub fn getTokenX(poolKey: ByteVec) -> ByteVec {
        return Pool(subContractId!(poolKey)).getTokenX()
    }

    pub fn getTokenY(poolKey: ByteVec) -> ByteVec {
        return Pool(subContractId!(poolKey)).getTokenY()
    }

    pub fn getTickSpacing(poolKey: ByteVec) -> U256 {
        return Pool(subContractId!(poolKey)).getTickSpacing()
    }

    pub fn wrappedAddFee(poolKey: ByteVec, feeAmount: U256, xToY: Bool, fee: U256) -> () {
        let callerId = callerAddress!()
        checkCaller!((callerId == invariantId || callerId == positionsId || callerId == swapUtilsId) && areAdminsSet, PoolsError.NotAdmin)

        Pool(subContractId!(poolKey)).addFee(feeAmount, xToY, fee)
    }

    pub fn setSqrtPrice(poolKey: ByteVec, sqrtPrice: U256) -> () {
        let callerId = callerAddress!()
        checkCaller!((callerId == invariantId || callerId == positionsId || callerId == swapUtilsId) && areAdminsSet, PoolsError.NotAdmin)

        Pool(subContractId!(poolKey)).setSqrtPrice(sqrtPrice)
    }

    pub fn setTickIndex(poolKey: ByteVec, v: I256) -> () {
        let callerId = callerAddress!()
        checkCaller!((callerId == invariantId || callerId == positionsId || callerId == swapUtilsId) && areAdminsSet, PoolsError.NotAdmin)

        Pool(subContractId!(poolKey)).setPoolCurrentTickIndex(v)
    }

    pub fn wrappedCrossTick(
        poolKey: ByteVec,
        nextSqrtPrice: U256,
        swapLimit: U256,
        tickIndex: I256,
        remainingAmount: U256,
        byAmountIn: Bool,
        xToY: Bool,
        protocolFee: U256,
        fee: U256,
        tickSpacing: U256
    ) -> (U256, U256, Bool) {
        let callerId = callerAddress!()
        checkCaller!((callerId == invariantId || callerId == positionsId || callerId == swapUtilsId) && areAdminsSet, PoolsError.NotAdmin)

        return Pool(subContractId!(poolKey)).crossTick(
            nextSqrtPrice,
            swapLimit,
            tickIndex,
            remainingAmount,
            byAmountIn,
            xToY,
            protocolFee,
            fee,
            tickSpacing
        )
    }

    pub fn wrappedCrossLiquidityUpdate(
        poolKey: ByteVec,
        add: Bool,
        liquidityDelta: U256
    ) -> () {
        let callerId = callerAddress!()
        checkCaller!((callerId == invariantId || callerId == positionsId || callerId == swapUtilsId) && areAdminsSet, PoolsError.NotAdmin)

        return Pool(subContractId!(poolKey)).crossLiquidityUpdate(add, liquidityDelta)
    }

    pub fn setLastTimestamp(poolKey: ByteVec, timestamp: U256) -> () {
        let callerId = callerAddress!()
        checkCaller!((callerId == invariantId || callerId == positionsId || callerId == swapUtilsId) && areAdminsSet, PoolsError.NotAdmin)

        Pool(subContractId!(poolKey)).setLastTimestamp(timestamp)
    }

    pub fn updateLiquidity(poolKey: ByteVec, liquidityDelta: U256, liquiditySign: Bool, upperTick: I256, lowerTick: I256) -> (U256, U256) {
        let callerId = callerAddress!()
        checkCaller!((callerId == invariantId || callerId == positionsId || callerId == swapUtilsId) && areAdminsSet, PoolsError.NotAdmin)


        return Pool(subContractId!(poolKey)).updateLiquidity(liquidityDelta, liquiditySign, upperTick, lowerTick)
    }
    
    pub fn get(poolKey: ByteVec) -> (Bool, U256, U256, I256, U256, U256, U256, U256, U256, U256, Address) {
        let subContractId = subContractId!(poolKey)

        if (!contractExists!(subContractId)) {
            return false, 0, 0, 0i, 0, 0, 0, 0, 0, 0, byteVecToAddress!(b``)
        } else {
            return true, Pool(subContractId).get()
        }
    }
}