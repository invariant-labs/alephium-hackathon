Abstract Contract FeeTiers() {
    @using(checkExternalCaller = false, updateFields = true, preapprovedAssets = true)
    pub fn wrappedAddFeeTier(originalCaller: Address, fee: U256, tickSpacing: U256) -> () {

        let exists = containsFeeTier(fee, tickSpacing)
        assert!(!exists, InvariantError.FeeTierAlreadyExist)    

        feeTierCount = feeTierCount + 1
        feeTiers.insert!(originalCaller, feeTierCount, FeeTier { fee: fee, tickSpacing: tickSpacing })
    }


    @using(checkExternalCaller = false, updateFields = true)
    pub fn wrappedRemoveFeeTier(originalCaller: Address, fee: U256, tickSpacing: U256) -> () {
        for (let mut i = 1; i <= feeTierCount; i = i + 1) {
            let state = feeTiers[i]
            if (fee == state.fee && tickSpacing == state.tickSpacing) {
                // FeeTier is last in the list
                if (i == feeTierCount) {
                    feeTiers.remove!(originalCaller, i)
                } else {
                    let lastState = feeTiers[feeTierCount]
                    feeTiers[i] = lastState
                    feeTiers.remove!(originalCaller, feeTierCount)
                }
                feeTierCount = feeTierCount - 1
                return
            }
        }

        assert!(false, InvariantError.FeeTierNotFound)
    }

    pub fn containsFeeTier(fee: U256, tickSpacing: U256) -> Bool {
        for (let mut i = 1; i <= feeTierCount; i = i + 1) {
            let state = feeTiers[i]
        
            if (fee == state.fee && tickSpacing == state.tickSpacing) {
                return true
            }
        }

        return false
    }

    pub fn countFeeTiers() -> U256 {
        return feeTierCount
    }
    
    pub fn getAllFeeTiers() -> ByteVec {
        let mut feeTiersBytes = b``

        for (let mut i = 1; i <= feeTierCount; i = i + 1) {
            let state = feeTiers[i]
            feeTiersBytes = feeTiersBytes ++ toByteVec!(state.fee) ++ b`break` ++ toByteVec!(state.tickSpacing) ++ b`break`
        }

        return feeTiersBytes
    }
}