Contract Tickmap(
    chunkTemplateContractId: ByteVec,
    clammContractId: ByteVec,
    mut invariantId: ByteVec,
    mut swapUtilsId: ByteVec,
    mut areAdminsSet: Bool
) {
    enum TickmapErrors {
        NotAdmin = 600
        InvalidTickIndex = 601
        TickAndTickSpacingMismatch = 602
        LimitReached = 603
        ChunkNotFound = 604
        TickInitialized = 605
        AdminsAreSet = 606
    }

    @using(updateFields = true, checkExternalCaller = false)
    pub fn setAdmins(invariant: ByteVec, swapUtils: ByteVec) -> () {
        assert!(!areAdminsSet, TickmapErrors.AdminsAreSet)
        invariantId = invariant
        swapUtilsId = swapUtils
        areAdminsSet = true
    }

    fn getChunkSize() -> U256 {
        return 64
    }

    fn getTickSearchRange() -> I256 {
        return 256i
    }

    fn tickToPosition(tick: I256, tickSpacing: U256) -> (U256, U256) {
        assert!(tick >= CLAMM(clammContractId).getGlobalMinTick() && tick <= CLAMM(clammContractId).getGlobalMaxTick(), TickmapErrors.InvalidTickIndex)
        assert!(tick % toI256!(tickSpacing) == 0i, TickmapErrors.TickAndTickSpacingMismatch)
    
        let bitmapIndex = toU256!(tick + CLAMM(clammContractId).getGlobalMaxTick()) / tickSpacing
    
        let chunk = bitmapIndex / getChunkSize()
        let bit = bitmapIndex % getChunkSize()
    
        return chunk, bit
    }

    fn getBitAtPosition(value: U256, position: U256) -> Bool {
        return (value >> position) & 1 == 1
    }

    fn flipBitAtPosition(value: U256, position: U256) -> U256 {
        return value ^ (1 << position)
    }


    fn getSearchLimit(tick: I256, tickSpacing: U256, up: Bool) -> I256 {
        let index = tick / toI256!(tickSpacing)

        let mut limit = 0i

        if (up) {
            let rangeLimitUp = index + getTickSearchRange()
            let sqrtPriceLimitUp = CLAMM(clammContractId).getGlobalMaxTick() / toI256!(tickSpacing)

            if (rangeLimitUp < sqrtPriceLimitUp) {
                limit = rangeLimitUp
            } else {
                limit = sqrtPriceLimitUp
            }
        } else {
            let rangeLimitDown = index - getTickSearchRange()
            let sqrtPriceLimitDown = CLAMM(clammContractId).getGlobalMinTick() / toI256!(tickSpacing)

            if (rangeLimitDown > sqrtPriceLimitDown) {
                limit = rangeLimitDown
            } else {
                limit = sqrtPriceLimitDown
            }
        }

        return limit * toI256!(tickSpacing)
    }

    fn nextInitialized(tick: I256, tickSpacing: U256, poolKey: ByteVec) -> (Bool, I256) {
        let limit = getSearchLimit(tick, tickSpacing, true)

        if (tick + toI256!(tickSpacing) > CLAMM(clammContractId).getGlobalMaxTick()) {
            return false, 0i
        }

        let (mut chunk, mut bit) = tickToPosition(tick + toI256!(tickSpacing), tickSpacing)
        let (limitingChunk, limitingBit) = tickToPosition(limit, tickSpacing)

        while (chunk < limitingChunk || (chunk == limitingChunk && bit <= limitingChunk)) {
            let mut shifted = getChunk(chunk, poolKey) >> bit

            if (shifted != 0) {
                while (shifted % 2 == 0) {
                    shifted = shifted >> 1
                    bit = bit + 1
                }

                if (chunk < limitingChunk || (chunk == limitingChunk && bit <= limitingBit)) {
                    let index = chunk * getChunkSize() + bit
                    
                    return true, (toI256!(index) - (CLAMM(clammContractId).getGlobalMaxTick() / toI256!(tickSpacing))) * toI256!(tickSpacing)
                } else {
                    return false, 0i
                }
            }

            chunk = chunk + 1
            bit = 0
        }

        return false, 0i
    }

    fn prevInitialized(tick: I256, tickSpacing: U256, poolKey: ByteVec) -> (Bool, I256) {
        let limit = getSearchLimit(tick, tickSpacing, false)
        let (mut chunk, mut bit) = tickToPosition(tick, tickSpacing)
        let (limitingChunk, limitingBit) = tickToPosition(limit, tickSpacing)

        while (chunk > limitingChunk || (chunk == limitingChunk && bit >= limitingBit)) {
            let mut mask = 1 << bit
            let value = getChunk(chunk, poolKey)

            if (value % (mask << 1) > 0) {
                while (value & mask == 0) {
                    mask = mask >> 1
                    bit = bit - 1
                }

                if (chunk > limitingChunk || (chunk == limitingChunk && bit >= limitingBit)) {
                    let index = chunk * getChunkSize() + bit
                    return true, (toI256!(index) - (CLAMM(clammContractId).getGlobalMaxTick() / toI256!(tickSpacing))) * toI256!(tickSpacing)
                } else {
                    return false, 0i
                }
            }

            chunk = chunk - 1
            bit = getChunkSize() - 1
        }

        return false, 0i
    }

    pub fn getCloserLimit(sqrtPriceLimit: U256, xToY: Bool, currentTick: I256, tickSpacing: U256, poolKey: ByteVec) -> (U256, Bool, I256, Bool) {
        let callerId = toByteVec!(callerAddress!())
        assert!((callerId == invariantId || callerId == swapUtilsId) && areAdminsSet, TickmapErrors.NotAdmin)

        let mut closesTickBool = false
        let mut closesTickIndex = 0i
        
        if (xToY) {
            let (closesTickBoolXtoY, closesTickIndexXtoY) = prevInitialized(currentTick, tickSpacing, poolKey)
            closesTickBool = closesTickBoolXtoY
            closesTickIndex = closesTickIndexXtoY
        } else {
            let (closesTickBoolYtoX, closesTickIndexYtoX) = nextInitialized(currentTick, tickSpacing, poolKey)
            closesTickBool = closesTickBoolYtoX
            closesTickIndex = closesTickIndexYtoX
        }

        if (closesTickBool) {
            let sqrtPriceExist = CLAMM(clammContractId).calculateSqrtPrice(closesTickIndex)

            if ((xToY && sqrtPriceExist > sqrtPriceLimit) || (!xToY && sqrtPriceExist < sqrtPriceLimit)) {
                return sqrtPriceExist, true, closesTickIndex, true
            } else {
                return sqrtPriceExist, false, 0i, false
            }
        } else {
            let index = getSearchLimit(currentTick, tickSpacing, !xToY)
            let sqrtPriceNotExist = CLAMM(clammContractId).calculateSqrtPrice(index)

            assert!(currentTick != index, TickmapErrors.LimitReached)

            if ((xToY && sqrtPriceNotExist > sqrtPriceLimit) || (!xToY && sqrtPriceNotExist < sqrtPriceLimit)) {
                return sqrtPriceNotExist, true, index, false
            } else {
                return sqrtPriceNotExist, false, 0i, false
            }
        }
    }

    fn getChunk(chunk: U256, poolKey: ByteVec) -> U256 {
        let subContractId = subContractId!(poolKey ++ toByteVec!(chunk))
        let exists = contractExists!(subContractId)

        if (!exists) {
            return 0
        } else {
            let contract = Chunk(subContractId)
            return contract.get()
        }
    }

    pub fn get(tick: I256, tickSpacing: U256, poolKey: ByteVec) -> Bool {
        let (chunk, bit) = tickToPosition(tick, tickSpacing)

        return getBitAtPosition(getChunk(chunk, poolKey), bit)
    }

    @using(preapprovedAssets = false, checkExternalCaller = false)
    pub fn flip(value: Bool, tick: I256, tickSpacing: U256, poolKey: ByteVec) -> () {
        let callerId = toByteVec!(callerAddress!())
        assert!((callerId == invariantId || callerId == swapUtilsId) && areAdminsSet, TickmapErrors.NotAdmin)

        let (chunk, bit) = tickToPosition(tick, tickSpacing)
        let isInitialized = getBitAtPosition(chunk, bit)
        let id = poolKey ++ toByteVec!(chunk)
        let subContractId = subContractId!(id)
        assert!(contractExists!(subContractId), TickmapErrors.ChunkNotFound)
        let contract = Chunk(subContractId)

        assert!(value == true && isInitialized == false || value == false && isInitialized == true, TickmapErrors.TickInitialized)

        contract.set(flipBitAtPosition(contract.get(), bit))
    }

    @using(preapprovedAssets = true, checkExternalCaller = false)
    pub fn initializeChunk(caller: Address, poolKey: ByteVec, chunk: U256) -> () {
        let callerId = toByteVec!(callerAddress!())
        assert!((callerId == invariantId || callerId == swapUtilsId) && areAdminsSet, TickmapErrors.NotAdmin)

        let id = poolKey ++ toByteVec!(chunk)        
        let exists = contractExists!(subContractId!(id))

        if(!exists) {
            let (encodedImmFields, encodedMutFields) = Chunk.encodeFields!(0)
            copyCreateSubContract!{caller -> ALPH: 1 alph}(
                id,
                chunkTemplateContractId,
                encodedImmFields,
                encodedMutFields
            )
        }
    }

    @using(checkExternalCaller = false)
    pub fn getChunks(lowerTick: I256, upperTick: I256, tickSpacing: U256) -> (U256, U256) {
        let (lowerChunk, _) = tickToPosition(lowerTick, tickSpacing)
        let (upperChunk, _) = tickToPosition(upperTick, tickSpacing)
        return lowerChunk, upperChunk
    }
}