Contract Ticks(
    tickTemplateContractId: ByteVec,
    mut invariantId: Address,
    mut areAdminsSet: Bool
) {
    enum TicksError {
        NotAdmin = 700
        AdminsAreSet = 701
        TickAlreadyExist = 702
    }

    @using(updateFields = true, checkExternalCaller = false)
    pub fn setAdmins(invariant: Address) -> () {
        assert!(!areAdminsSet, TicksError.AdminsAreSet)
        invariantId = invariant
        areAdminsSet = true
    }

    @using(preapprovedAssets = true)
    pub fn add(
        originalCaller: Address,
        poolKey: ByteVec,
        index: I256,
        sign: Bool,
        liquidityChange: U256,
        liquidityGross: U256,
        sqrtPrice: U256,
        feeGrowthOutsideX: U256,
        feeGrowthOutsideY: U256,
        secondsOutside: U256
    ) -> () {
        let caller = callerAddress!()
        checkCaller!((caller == invariantId) && areAdminsSet, TicksError.NotAdmin)

        let path = poolKey ++ toByteVec!(index)
        assert!(!contractExists!(subContractId!(path)), TicksError.TickAlreadyExist)

        let (encodedImmFields, encodedMutFields) = Tick.encodeFields!(
            selfAddress!(),
            TickState {
                sign: sign,
                liquidityChange: liquidityChange,
                liquidityGross: liquidityGross,
                sqrtPrice: sqrtPrice,
                feeGrowthOutsideX: feeGrowthOutsideX,
                feeGrowthOutsideY: feeGrowthOutsideY,
                secondsOutside: secondsOutside
            }
        )
        let _ = copyCreateSubContract!{originalCaller -> ALPH: 1 alph}(
            path,
            tickTemplateContractId,
            encodedImmFields,
            encodedMutFields
        )
    }

    pub fn contains(
        poolKey: ByteVec,
        index: I256
    ) -> Bool {
        return contractExists!(subContractId!(poolKey ++ toByteVec!(index)))
    }

    pub fn updateTick(poolKey: ByteVec, index: I256, liquidityDelta: U256, maxLiquidityPerTick: U256, isUpper: Bool, isDeposit: Bool) -> () {
        let caller = callerAddress!()
        checkCaller!((caller == invariantId) && areAdminsSet, TicksError.NotAdmin)

        Tick(subContractId!(poolKey ++ toByteVec!(index))).updateTick(liquidityDelta, maxLiquidityPerTick, isUpper, isDeposit)
    }

    pub fn getFeeGrowthOutside(poolKey: ByteVec, index: I256) -> (U256, U256) {
        let contract = Tick(subContractId!(poolKey ++ toByteVec!(index)))
        return contract.getFeeGrowthOutsideX(), contract.getFeeGrowthOutsideY()
    }

    pub fn getLiquidityChange(poolKey: ByteVec, index: I256) -> U256 {
        return Tick(subContractId!(poolKey ++ toByteVec!(index))).getLiquidityChange()
    }

    pub fn getLiquidityGross(poolKey: ByteVec, index: I256) -> U256 {
        return Tick(subContractId!(poolKey ++ toByteVec!(index))).getLiquidityGross()
    }

    pub fn get(poolKey: ByteVec, index: I256) -> (Bool, TickState) {
        let subContractId = subContractId!(poolKey ++ toByteVec!(index))

        if (!contractExists!(subContractId)) {
            return
            false,
            TickState {
                sign: false,
                liquidityChange: 0,
                liquidityGross: 0,
                sqrtPrice: 0,
                feeGrowthOutsideX: 0,
                feeGrowthOutsideY: 0,
                secondsOutside: 0
            }
        } else {
            return true, Tick(subContractId).get()
        }
    }

    pub fn wrappedCross(poolKey: ByteVec, index: I256, feeGrowthGlobalX: U256, feeGrowthGlobalY: U256, currentTick: I256) -> (Bool, U256) {
        let caller = callerAddress!()
        checkCaller!((caller == invariantId) && areAdminsSet, TicksError.NotAdmin)

        return Tick(subContractId!(poolKey ++ toByteVec!(index))).cross(feeGrowthGlobalX, feeGrowthGlobalY, currentTick, index)
    }
}